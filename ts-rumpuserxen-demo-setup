#!/usr/bin/perl -w
# This is part of "osstest", an automated testing framework for Xen.
# Copyright (C) 2009-2013 Citrix Inc.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict qw(vars);
use DBI;
use Osstest;
use Osstest::TestSupport;

tsreadconfig();

our ($whhost,$gn) = @ARGV;
$whhost ||= 'host';
$gn ||= 'rump';

our $ho= selecthost($whhost);

our $gho;

our $rkdist;

sub prep () {
    $gho = prepareguest($ho, $gn, $gn, 4096, undef, 30);

    my $cfg_rkdist = target_extract_jobdistpath_subdir
	($ho, 'rumpuserxen--base', 'rumpuserxen',
	 $r{guests_rumpuserxenbuildjob});
    $cfg_rkdist .= '/usr/local/lib/xen/rump-kernel';

    my $buildjob = guest_var($gho, 'rumpuserxenbuildjob', undef);
    my $builtimage = guest_var($gho, 'builtimage', undef);
    $builtimage =~ m/\:/ or die "$builtimage ?";
    my $builtimage_part = $`;
    my $builtimage_subpath = $'; #';
    $rkdist = target_extract_jobdistpath_subdir
	($ho, "rumpuserxen-$gn", $builtimage_part, $buildjob);
    my $imagefile = $rkdist.$builtimage_subpath;

    my @images;

    my $fsimagesdir = target_jobdir($ho)."/$gn-images";

    my $cfgfile = "$gn.cfg";
    my $cfgpath = "/etc/xen/$cfgfile";
    target_editfile_root($ho, "$cfg_rkdist/domain_config", $cfgfile, $cfgpath,
			 sub {
	while (<EI>) {
	    next if m/^\s*\#/;
	    next unless m/\S/;

	    my $in = $& if m/^\w\S*/;

	    s# = .*              # " = '$gn'"          #xe  if $in eq 'name';
	    s# = .*              # " = '$imagefile'"   #xe  if $in eq 'kernel';
	    s#\b mac=[0-9a-f:]+  # "mac=$gho->{Ether}" #xe  if $in eq 'vif';
	    s#\b 4             \b# 3                   #xe  if $in eq 'extra';

	    if ($in eq 'disk') {
		s{\b img/(\w+.ffs) \b}{
                    push @images, $1;
                    "$fsimagesdir/$1";
                }xeg;
	    }

	    print EO or die $!;
	}
    });

    store_runvar("$gho->{Guest}_cfgpath", $cfgpath);

    target_cmd($ho, <<END.(join "\n", map { <<END; } @images), 200);
                   mkdir -p $fsimagesdir
END
                   cp $cfg_rkdist/img/$_ $fsimagesdir/$_
END
}

prep();
