#!/usr/bin/tclsh8.4
# -*- Tcl -*- 
# usage: ./sg-execute-flight FLIGHT BLESSING

source osstestlib.tcl

readconfig

proc check {} {
    global flight jobinfo running blessing

    set nqueued [pg_execute dbh "
        SELECT job FROM jobs j
         WHERE j.flight = $flight
           AND j.status = 'queued'
         LIMIT 1
    "]

    set nrunning [llength $running]
    log "flight $flight nqueued=$nqueued nrunning=$nrunning"

    if {!$nqueued && !$nrunning} {
        log "flight $flight ending."
        exec ./cs-flight-bless $flight $blessing 2>@ stderr
        exit 0
    }

    pg_execute -array jobinfo dbh "
        SELECT * FROM jobs j
         WHERE j.flight = $flight
           AND j.status = 'queued'
           AND 0 = (SELECT count(*) FROM jobs d
                     WHERE d.flight = $flight
                       AND ( d.status = 'queued'
                          OR d.status = 'preparing'
                          OR d.status = 'running'
                          OR d.status = 'retriable' )
                       AND d.job IN (SELECT val FROM runvars r
                                     WHERE r.flight = $flight
                                       AND r.job = j.job
                                       AND r.name LIKE '%job'
                                       AND r.val NOT LIKE '%.%')
                   )
         ORDER BY job
    " maybe-spawn-job
}

proc maybe-spawn-job {} {
    global flight jobinfo running

    foreach entry $running {
        lassign $entry pid job
        if {[string equal $job $jobinfo(job)]} return
    }

    switch -glob $jobinfo(recipe) {
        build* { set runner sg-run-builds }
        test* { set runner sg-run-tests }
        * { error "unknown runner for recipe $jobinfo(recipe)" }
    }
    log "flight $flight spawning $jobinfo(job) $jobinfo(recipe) $runner"

    flush stdout
    flush stderr
    set pid [fork]
    if {!$pid} {
        execl ./$runner [list $flight --jobs=$jobinfo(job)]
        error "execl returned success ?!"
    }
    lappend running [list $pid $jobinfo(job)]
    log "flight $flight spawned  $jobinfo(job) \[$pid\]"
}

proc main_iteration {} {
    global flight running nqueued jobinfo

    check

    if {![llength running]} {
        error "nothing running, but queued not ready"
    }
    lassign [wait] gotpid how st

    set ix 0
    foreach entry $running {
        lassign $entry pid job
        if {$gotpid == $pid} break
        incr ix
    }
    if {![info exists pid]} {
        log "unexpected child \[$pid\] $how $st"
        return
    }
    
    log "flight $flight reaped  $job \[$pid\] $how $st"
    transaction flights {
        set found [pg_execute -array jobinfo dbh "
            SELECT * FROM jobs
                    WHERE flight=$flight
                      AND job=[pg_quote $job]
        "]
        if {!$found} { error "job not in database $flight $job" }
        if {[string equal $jobinfo(status) queued]} {
            if {![string compare EXIT $how] && $st==0} {
                error "job still queued $flight $job"
            } else {
                job-set-status $flight $job broken
            }
        }
    }

    lvarpop running $ix
}

proc parse-args {} {
    global argv blessing
    set-flight
    if {[llength $argv]!=1} { error "wrong # args" }
    lassign $argv blessing
}

proc main {} {
    global running flight
    set running {}
    while 1 main_iteration
}

parse-args
db-open
main
