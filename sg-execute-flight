#!/usr/bin/tclsh8.4
# -*- Tcl -*- 
# usage: ./sg-execute-flight FLIGHT BLESSING

source osstestlib.tcl

readconfig

proc check {} {
    global flight jobinfo running blessing killing stopping

    if {$stopping} return

    set nqueued [pg_execute dbh "
        SELECT job FROM jobs j
         WHERE j.flight = $flight
           AND j.status = 'queued'
         LIMIT 1
    "]

    set nrunning [llength $running]
    log "flight $flight nqueued=$nqueued nrunning=$nrunning"

    if {!$nqueued && !$nrunning} {
        prequit finished
        exec ./cs-flight-bless $flight $blessing running 2>@ stderr
        exit 0
    }

    pg_execute -array jobinfo dbh "
        SELECT * FROM jobs j
         WHERE j.flight = $flight
           AND j.status = 'queued'
           AND 0 = (SELECT count(*) FROM jobs d
                     WHERE d.flight = $flight
                       AND ( d.status = 'queued'
                          OR d.status = 'preparing'
                          OR d.status = 'running'
                          OR d.status = 'retriable' )
                       AND (d.job IN (SELECT val FROM runvars r
                                     WHERE r.flight = $flight
                                       AND r.job = j.job
                                       AND r.name LIKE '%job')
                        OR (d.flight || '.' || d.job) IN
                                     (SELECT val FROM runvars r
                                     WHERE r.flight = $flight
                                       AND r.job = j.job
                                       AND r.name LIKE '%job'))
                   )
         ORDER BY job
    " maybe-spawn-job
}

proc prequit {why} {
    global killing flight blessing
    log "flight $flight ending $why."
    while {$killing} { vwait killing }
    log "flight $flight ended $why."
}

proc maybe-spawn-job {} {
    global flight jobinfo running forever

    foreach entry $running {
        lassign $entry pid job
        if {[string equal $job $jobinfo(job)]} return
    }

    switch -glob $jobinfo(recipe) {
        build* { set runner sg-run-builds }
        test* { set runner sg-run-tests }
        * { error "unknown runner for recipe $jobinfo(recipe)" }
    }
    log "flight $flight spawning $jobinfo(job) $jobinfo(recipe) $runner"

    pipe waiter_sentinel_r waiter_sentinel_w
    fcntl $waiter_sentinel_r CLOEXEC 1
    fcntl $waiter_sentinel_w CLOEXEC 1

    flush stdout
    flush stderr
    set pid [fork]

    if {!$pid} {
        id process group set
        if {![fork]} {
            # this process exists to ensure the process group
            # survives, and therefore that the pid is not reused,
            # until sg-execute-flight sends the group SIGKILL
            dup $waiter_sentinel_r stdin
            execl sh {-c {trap '' TERM; read dummy}}
            error "execl sh returned success ?!"
        }
        execl ./$runner [list $flight --jobs=$jobinfo(job)]
        error "execl returned success ?!"
    }
    close $waiter_sentinel_r
    # we leak $waiter_sentinel_w at job exit, but we don't really care
    lappend running [list $pid $jobinfo(job)]
    log "flight $flight spawned  $jobinfo(job) \[$pid\]"
}

proc kill-term {job pid} {
    global killing
    kill -pgroup SIGTERM $pid
    incr killing
    after 45000 [list kill-kill $job $pid]
}

proc shutdown {why} {
    global running stopping
    set stopping 1
    signal default {SIGTERM SIGINT SIGHUP}
    log "shutdown $why"
    foreach entry $running {
        lassign $entry pid job
        kill-term $job $pid
    }
    prequit "shutdown $why"
    exit 0
}

proc main_iteration {} {
    global flight running nqueued jobinfo

    update

    check

    update

    if {![llength running]} {
        error "nothing running, but queued not ready"
    }
    while 1 {
        set w [wait -nohang]
        if {[llength $w]} break
        update
        after 1000
        update
    }
    lassign $w gotpid how st

    set ix 0
    foreach entry $running {
        lassign $entry pid job
        if {$gotpid == $pid} break
        incr ix
    }
    if {![info exists pid]} {
        log "unexpected child \[$pid\] $how $st"
        return
    }

    set exitok [expr {![string compare EXIT $how] && $st==0}]
    
    log "flight $flight reaped  $job \[$pid\] $how $st"

    kill-term $job $pid

    transaction flights {
        set found [pg_execute -array jobinfo dbh "
            SELECT * FROM jobs
                    WHERE flight=$flight
                      AND job=[pg_quote $job]
        "]
        if {!$found} { error "job not in database $flight $job" }

        switch -exact -- $jobinfo(status) {
            pass - fail - broken - blocked {
            }
            queued - preparing - running {
                job-set-status-unlocked $flight $job broken
                if {$exitok} {
                    error "job $flight.$job runner exited 0\
                       but left job status $jobinfo(status)"
                }
            }
            default {
                error "job $flight.$job runner ($how $st)\
                       left unexpected job status\
                       $jobinfo(status)"
            }
        }
    }

    lvarpop running $ix
}

proc kill-kill {job pid} {
    global killing flight
    incr killing -1
    log "flight $flight killing $job \[$pid\]"
    kill -pgroup SIGKILL $pid
}

proc parse-args {} {
    global argv blessing
    set-flight
    if {[llength $argv]!=1} { error "wrong # args" }
    lassign $argv blessing
}

proc main {} {
    global running flight killing stopping
    set killing 0
    set stopping 0
    set running {}
    signal trap {SIGTERM SIGINT SIGHUP} {shutdown {%S}}
    while 1 main_iteration
}

parse-args
db-open
main
