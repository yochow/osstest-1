#!/usr/bin/perl -w
#
# Compute the best combination of things to test for another
# bisection step.  Create a flight with appropriate builds and
# tests.
#
# usage:
#   ./cs-bisection-setup [<options>] <branch> <job> <testid>
#      --blessings=real,real-bisect

use strict qw(vars);
use Osstest;
use Data::Dumper;
no warnings qw(recursion);

our @blessings= qw(real real-bisect);
our @revtuplegenargs= ();

my $graphfile= 'bisection';
my $popflight= 0;
my $debug= 0;

our $worst_estatus= -1;
our $estatus= 255;
END { $?= $estatus; }

# exit status
#   0    have identified a revision to test
#   4    bisection complete and successful
#   8    bisection finished and inconclusive
#  12    bisection finished, but inconsistent test results cast doubt
#  16    fatally inconsistent test results
# 255    doom and disaster

open DEBUG, ">/dev/null" or die $!;
open SUMMARY, ">/dev/null" or die $!;

while (@ARGV && $ARGV[0] =~ m/^\-/) {
    $_= shift @ARGV;
    last if $_ eq '--';
    if (s/^--blessings=//) {
	@blessings= split /\,/;
	next;
    } elsif (m/^--graph-out=(.+)$/) {
        $graphfile= $1;
    } elsif (m/^--no-update$/) {
        push @revtuplegenargs, '-N';
    } elsif (m/^--summary-out=(.+)$/) {
        open SUMMARY, "> $1" or die $!;
    } elsif (m/^--no-graph-out$/) {
        $graphfile= '';
    } elsif (m/^--flight=(\d+)$/) {
        $popflight= $1;
    } elsif (m/^-D$/) {
        $debug++;
        if ($debug==1) {
            open DEBUG, ">&2" or die $!;
            DEBUG->autoflush(1);
        }
        if ($debug==3) {
            push @revtuplegenargs, '-D';
        }
    } else {
	die "$_ ?";
    }
}

@ARGV==3 or die;
our ($branch,$job,$testid) = @ARGV;

our ($latest_flight, $hosts, $basispass_flight);
our (@trees);

our $blessingscond= '('.join(" OR  ", map { "blessing='$_'" } @blessings).')';

sub psummary ($) {
    my ($s) = @_;
    print STDERR $s or die $!;
    print SUMMARY $s or die $!;
}

sub summary_report ($$$) {
    my ($heading,$body,$new_estatus) = @_;
    psummary("\n");
    if (length $heading) {
        chomp $heading;
        psummary("*** $heading ***\n");
    }
    if (length $body) {
        chomp $body;
        $body =~ s/^/  /gm;
        psummary("$body\n\n");
    }
    if ($new_estatus > $worst_estatus) {
        $worst_estatus= $new_estatus;
    }
}

sub relevant_hosts ($) {
    my ($flight) = @_;
    my $sth= $dbh_tests->prepare(<<END);
        SELECT name,val FROM runvars
	   WHERE job = ?
	     AND flight = ?
	     AND name LIKE '%host'
END
    $sth->execute($job,$latest_flight);
    return join ',', map { join '=', @$_ } @{ $sth->fetchall_arrayref() }
}

sub flight_has_relevant_hosts ($) {
    my ($flight) = @_;
    my $hosts= relevant_hosts($flight);
}

sub flight_rmap ($) {
    my ($flight) = @_;

    $dbh_tests->do(<<END, {}, $job, $flight);
    
        SELECT t.name AS use,
	       b.name AS name,
	       b.val  AS val,
	       b.job  AS job

          INTO TEMP TABLE tmp_build_info
	       
	  FROM runvars AS t
    CROSS JOIN runvars AS b
    
           WHERE t.job = ?
	     AND t.flight = ?
	     AND t.name LIKE '%buildjob'
	     AND ((t.val NOT LIKE '%.%' AND b.flight=t.flight AND t.val=b.job)
	       OR (t.val LIKE '%.%' AND t.val = b.flight || '.' || b.job))

END
    $dbh_tests->do(<<END, {}, $job, $flight);

        INSERT INTO tmp_build_info
	    SELECT ''   AS use,
	           name AS name,
	           val  AS val,
	           job  AS job
	      FROM runvars
	     WHERE job = ?
	       AND flight = ?
END

    my $sth= $dbh_tests->prepare(<<END);

        SELECT url.val AS uval,
	       rev.val AS rval,
	       url.job AS job

	    FROM tmp_build_info AS rev
      CROSS JOIN tmp_build_info AS url

           WHERE (rev.name LIKE E'built\\_revision\\_%' OR
                  rev.name LIKE E'revision\\_%')
  	     AND  url.name LIKE E'tree\\_%'
	     AND  rev.use = url.use
	     AND  rev.job = url.job
	     AND (rev.name = 'built_revision_' || substr(url.name,6) OR
                  rev.name = 'revision_'       || substr(url.name,6))

	   ORDER by url.val;
	  
END
    $sth->execute();
    my $row;
    my $mixed=0;
    my (@ttrees, @trevisions);
    while ($row= $sth->fetchrow_hashref()) {
	print DEBUG " $flight.$row->{job} uval=$row->{uval} rval=$row->{rval}\n";
        my $rev= $row->{rval};
        next unless length $rev;
        $rev =~ s/\+//g;
        $rev =~ s/^\d+://;
	if (@ttrees && $row->{uval} eq $ttrees[-1]) {
	    if ($rev ne $trevisions[-1]) {
		print STDERR "!! flight $flight mixed revisions for $ttrees[-1]: $trevisions[-1] and $rev\n";
		$mixed=1;
	    }
	} else {
	    push @ttrees, $row->{uval};
	    push @trevisions, $rev;
	}
    }
    $dbh_tests->do(<<END);
        DROP TABLE tmp_build_info
END
    return undef if $mixed;
    if (!@ttrees) {
	print STDERR "!! flight $flight no revisions or trees\n";
	return undef;
    }
    my $map = {};
    for (my $i=0; $i<@ttrees; $i++) {
        my $ttree= $ttrees[$i];
        my $trev= $trevisions[$i];
        die if exists $map->{ $ttree } and $map->{ $ttree } ne $trev;
        $map->{ $ttree }= $trev;
    }
    return $map;
}

sub flight_rtuple ($) {
    my ($flight) = @_;
    my $map= flight_rmap($flight);
    return () if !defined $map;
    my @revisions= ();
    die unless @trees;
    my $missing= 0;
    foreach my $tree (@trees) {
        my $revision= $map->{$tree};
        if (!defined $revision) {
            print STDERR "!! flight $flight missing revision for $tree\n";
            $missing++;
            next;
        }
        push @revisions, $revision;
    }
    return () if $missing;
    return @revisions;
}

sub findbasics () {
    my $sth= $dbh_tests->prepare(<<END);
	SELECT * FROM flights NATURAL JOIN steps
	   WHERE job = ?
	     AND testid = ?
	     AND blessing = ?
	     AND (status = 'fail' OR status = 'pass')
             AND branch = ?
	   ORDER BY started DESC
	   LIMIT 1
END
    $sth->execute($job,$testid,$blessings[0],$branch);
    my $row= $sth->fetchrow_hashref();
    die "no failure found" unless $row;
    die "last flight not a failure $row->{flight} $row->{status}"
        unless $row->{'status'} eq 'fail';
    $latest_flight= $row->{flight};
    print STDERR "Found failure, as expected, in flight $latest_flight\n";

    $hosts= relevant_hosts($latest_flight);
    print STDERR "Host specification list: $hosts\n";

    print STDERR "Searching for basis pass:";
    $sth= $dbh_tests->prepare(<<END);
        SELECT * FROM flights NATURAL JOIN steps
	   WHERE job = ?
	     AND testid = ?
	     AND blessing = ?
	     AND status = 'pass'
             AND branch = ?
	   ORDER BY started DESC
END
    $sth->execute($job,$testid,$blessings[0],$branch);
    while ($row= $sth->fetchrow_hashref()) {
	print STDERR " $row->{flight}";
	next unless flight_has_relevant_hosts($row->{flight});
	$basispass_flight= $row->{flight};
	last;
    }
    if (!$basispass_flight) {
	die "\nNo applicable basis pass, sorry!\n";
    }
    print STDERR ".\n";
}

our (%nodes, @latest_rtuple, @basispass_rtuple);

sub digraph_whole () {
    my $latest_rmap= flight_rmap($latest_flight);
    my $basispass_rmap= flight_rmap($basispass_flight);
    if (!defined $basispass_rmap) {
	die "Basis pass $basispass_flight rmap indeterminate/wrong\n";
    }

    foreach my $tree (sort keys %$latest_rmap) {
        if (defined $basispass_rmap->{$tree}) {
            push @trees, $tree;
        } else {
            print STDERR "(tree in latest but not in basispass: $tree)\n";
        }
    }
    foreach my $tree (sort keys %$basispass_rmap) {
        if (!defined $latest_rmap->{$tree}) {
            print STDERR "(tree in basispass but not in latest: $tree)\n";
        }
    }
    psummary("Tree: $_\n") foreach @trees;

    @latest_rtuple= flight_rtuple($latest_flight);
    @basispass_rtuple= flight_rtuple($basispass_flight);

    print STDERR "Latest @latest_rtuple\n";
    print STDERR "Basis pass @basispass_rtuple\n";

    my $cmd= "./adhoc-revtuple-generator @revtuplegenargs";
    foreach (my $i=0; $i<@trees; $i++) {
        $cmd.= ' '.$trees[$i].'#'.$basispass_rtuple[$i].'-'.$latest_rtuple[$i];
    }
    print STDERR "Generating revisions with $cmd\n";
    open REVLIST, "$cmd |" or die $!;
    while (<REVLIST>) {
        chomp;
        last if $_ eq '.';
        my $rtuple= $_;
        $!=0; $_=<REVLIST>;
        m/^(\d+) (\d+)$/ or die "$! $_ ?";
        $nodes{$rtuple}{Date}= $1;
        my $n= $nodes{$rtuple};
        my $nparents= $2;
        while ($nparents--) {
            $!=0; $_=<REVLIST>;
            chomp or die $!;
            push @{ $nodes{$_}{Children} }, $n;
            push @{ $n->{Parents} }, $nodes{$_};
        }
    }
    $!=0; $?=0; close REVLIST or die "$? $!";

    foreach my $rtuple (keys %nodes) {
        $nodes{$rtuple}{Rtuple}= $rtuple;
    }
    print STDERR "Loaded ".scalar(keys %nodes)." nodes in revision graph\n";

#print DEBUG Dumper(\%nodes),"\n";
}

sub walk ($$$;$);
sub walk ($$$;$) {
    my ($n, $parch, $code, $depth) = @_;
    return unless defined $n;
    $depth=0 unless defined $depth;
    return if $code->($n, $depth);
    foreach my $neigh (@{ $n->{$parch} }) {
        walk($neigh, $parch, $code, $depth+1);
    }
}

sub digraph_relevant () {
    my $paint_relevant;
    $paint_relevant= sub {
        my ($start, $parch, $paint) = @_;
        walk($start, $parch, sub {
            my ($n, $depth) = @_;
            #print DEBUG "PAINT_RELEVANT $parch $paint $depth $n->{Rtuple}\n";
            return $n->{$paint}++;
        });
    };
    $paint_relevant->($nodes{"@latest_rtuple"}, Parents, RelvDown);
    $paint_relevant->($nodes{"@basispass_rtuple"}, Children, RelvUp);

    foreach my $rtuple ("@latest_rtuple", "@basispass_rtuple") {
        foreach my $relflag (qw(RelvDown RelvUp)) {
            die "not $relflag at $rtuple" unless $nodes{$rtuple}{$relflag};
        }
    }
}

our @failures;

sub relevant ($) {
    my ($n) = @_;
    defined($n) and $n->{RelvDown} and $n->{RelvUp};
}

sub scantests () {
    my $basispass_date= $nodes{"@basispass_rtuple"}{Date};

    print STDERR "Searching for test results:\n";

    my $jobq= $dbh_tests->prepare("
       SELECT * FROM flights NATURAL JOIN jobs
          WHERE job = ?
            AND $blessingscond
            AND started >= $basispass_date
            AND branch = ?
    ");
    my $stepq= $dbh_tests->prepare("
       SELECT * FROM steps
          WHERE flight = ?
            AND job = ?
            AND testid = ?
    ");

    $jobq->execute($job,$branch);
    while (my $jobrow= $jobq->fetchrow_hashref()) {
        my @rtuple= flight_rtuple($jobrow->{flight});
	next unless @rtuple;

        $stepq->execute($jobrow->{flight},$job,$testid);
        my $steprow= $stepq->fetchrow_hashref();
        my $st= $steprow ? $steprow->{status} : 'blocked';
        print STDERR " $jobrow->{flight} $st ";
        my $n= $nodes{"@rtuple"};
        if (!relevant($n)) {
            print STDERR "irrelevant\n";
            next;
        }
        print STDERR "@rtuple\n";

        push @{ $n->{Flights} }, {
            Flight => $jobrow->{flight},
            Result => $st,
            Blessing => $jobrow->{blessing}
        };

        if ($st eq 'fail') {
            push @failures, $n;
            walk($n, Children, sub {
                my ($desc, $depth) = @_;
                return 0 unless $depth;
                return $desc->{UninterestingFailure}++;
            });
        }
    }
}

our @conflicted;

sub consolidateresults () {
    foreach my $n (values %nodes) {
        my $fs= $n->{Flights};
        my %r;
        $r{ $_->{Result} }=1 foreach @$fs;
        next unless %r;
        $n->{Result}= (keys %r)[0] if scalar(keys %r) == 1;
        if (scalar(keys %r) > 1) {
            $n->{Conflicted}= 1;
            push @conflicted, $n;
        }
    }
}

our @interesting_lengths;

sub search_compute_length_at ($);
sub search_compute_length_at ($) {
    my ($n) = @_;
    print DEBUG "CLA $n->{Rtuple} ";
    if (!relevant($n)) {
        print DEBUG "X (irrelevant)\n";
        return 'X';
    }
    if (defined $n->{Result}) {
        print DEBUG "0 ($n->{Result})\n";
        die "$n->{Rtuple} $n->{Result} " if $n->{Result} eq 'fail';
        if ($n->{Result} ne 'pass') {
            search_compute_length_below($n);
        }
        $n->{LengthAt}= 0;
        return 0;
    }
    my $res= 1;
    print DEBUG ".. x".scalar(@{ $n->{Parents} })."\n";
    foreach my $parent (@{ $n->{Parents} }) {
        my $t= search_compute_length_at($parent);
        next if $t eq 'X';
        $res += $t;
    }
    $n->{LengthAt}= $res;
    return $res;
}

sub search_compute_length_below ($) {
    my ($base) = @_;
    return if $base->{UninterestingFailure};
    print DEBUG "CLB $base->{Rtuple}\n";
    foreach my $n (@{ $base->{Parents} }) {
        next if exists $n->{InterestingLength};
        my $t= search_compute_length_at($n);
        next if $t eq 'X';
        $n->{InterestingLength}= $t;
        print STDERR " $t revisions at $n->{Rtuple}\n";
        push @interesting_lengths, $n;
    }
}

sub choose_revision_below ($$);
sub choose_revision_below ($$) {
    my ($n, $count) = @_;

    print DEBUG "CRB $count $n->{Rtuple}\n";

    return $n if $count <= 0;

    my $parents= $n->{Parents};
    die "reached end $n->{Rtuple} !" unless @$parents;

    my ($descend, @bypass) = sort {
        $b->{LengthAt} <=> $a->{LengthAt}
    } grep {
        defined $_->{LengthAt}
    } @$parents;
    # $bypass is the parent with the biggest LengthAt, so
    # the one we're going to investigate.

    $count--;
    $count -= $_->{LengthAt} foreach @bypass;

    return choose_revision_below($descend, $count);
}

our $choose;

sub do_choose_this ($) {
    my ($n) = @_;
    $choose= $n;
    print STDERR "Should test $choose->{Rtuple}.\n";
    $choose->{Target}= 1;
}

sub conflicted_warning ($$) {
    my ($n, $what) = @_;
    if ($n->{Conflicted}) {
        summary_report(<<END, <<END, 16);
Not reproducible - supposed $what
END
Could not reproduce problem, or could not reproduce basis pass.
Inconsistent test results at this revision (supposed $what):
$n->{Rtuple}
END
        $n->{Target}= 1;
        return 1;
    }
}

sub need_reproduce ($$) {
    my ($n, $what) = @_;

    return 1 if conflicted_warning($n, $what);
    
    my $fl= $n->{Flights} ;
    die "$what ?" unless @$fl;
    if (@$fl < 2) {
        print STDERR "Need to double-check by reproducing $what.\n";
        do_choose_this($n);
        return 1;
    }
    return 0;
}

sub search () {
    print STDERR "Searching for interesting versions\n";

    return if need_reproduce($nodes{"@latest_rtuple"}, "failure");
    return if need_reproduce($nodes{"@basispass_rtuple"}, "last pass");

    foreach my $startfail (@failures) {
        search_compute_length_below($startfail);
    }
    my $best;
    foreach my $n (@interesting_lengths) {
        next if defined $best
            and $best->{InterestingLength} >= $n->{InterestingLength};
        $best= $n;
    }
    if (!$best->{InterestingLength}) {
        print STDERR "No revisions left to test, checking graph state.\n";

        my $found_exact_failure= 0;
        my %faildone;

        foreach my $fail (@failures) {
            next if $faildone{$fail}++;

            print DEBUG " GS F $fail->{Rtuple}\n";
            if (conflicted_warning($fail, "first failure")) {
                print DEBUG " GS F $fail->{Rtuple} conflicted\n";
                next;
            }
            my @passparents;
            my $nope= 0;
            foreach my $pass (@{ $fail->{Parents} }) {
                print DEBUG " GS F $fail->{Rtuple} P $pass->{Rtuple}\n";
                $nope=1 if conflicted_warning($pass, "last pass");
                next unless defined $pass->{Result};
                print DEBUG " GS F $fail->{Rtuple} P $pass->{Rtuple}".
                    " R $pass->{Result}\n";
                next unless $pass->{Result} eq 'pass';
                ($nope=1),next if conflicted_warning($pass, "last pass");
                push @passparents, $pass;
            }
            next if $nope;
            next unless @passparents;
            
            return if need_reproduce($fail, "first failure");
            foreach my $pass (@passparents) {
                return if need_reproduce($pass, "last pass");
            }

            $found_exact_failure++;
            $fail->{Target}= 1;
            
            summary_report(<<END,'', 4);
Found and reproduced problem changeset
END
            my @failrev= split / /, $fail->{Rtuple};

            for (my $i=0; $i<@trees; $i++) {
                foreach my $pass (@passparents) {
                    my @passrev= split / /, $pass->{Rtuple};
                    next if $passrev[$i] eq $failrev[$i];

                    summary_report("",<<END, -1);
Bug is in tree:  $trees[$i]
Bug introduced:  $failrev[$i]
Bug not present: $passrev[$i]
END
                }
            }
        }
        if (!$found_exact_failure) {
            summary_report(<<END,<<END, 8);
Exact problem changeset not identified
END
See revision graph output for details.
END
        } elsif (@conflicted) {
            summary_report(<<END,<<END, 12);
HOWEVER - WARNING - INCONSISTENT TEST RESULTS
END
Some revisions produced unstable test results,
so interpret the results with caution.
See revision graph output for details.
END
        }
    } else {
        my $godown= ($best->{InterestingLength}) >> 1;
        print STDERR "Looking $godown below $best->{Rtuple}.\n";
        my $ch= choose_revision_below($best, $godown);
        do_choose_this($ch);
    }
}

sub gnodename ($) {
    my ($n) = @_;

    my $lab= $n->{Rtuple};
    $lab =~ s/(\S{12})\S+/$1/g;
    $lab =~ s/ /,/g;

    my $fs= $n->{Flights};
    if ($fs) {
        my $resultsdesc= join ' ', map {
            $_->{Flight}.':'.$_->{Result}
        } sort {
            $a->{Flight} <=> $b->{Flight}
        } @$fs;
        
        $lab.= "\\n".$resultsdesc;
    }
    return "\"$lab\"";
}

sub odot ($) { print DOT $_[0] or die $!; }

sub writegraph () {
    return unless length $graphfile;

    open DOT, "> $graphfile.dot" or die $!;
    
    odot(<<END);
digraph "$job $testid" {
  rankdir=BT;
  ranksep=0;
  nodesep=0.3;
  edge [arrowsize=0.2];
  node [shape=box, fontname=Courier, fontsize=9, style=filled];
END

    foreach my $n (values %nodes) {
        next unless relevant($n);

        odot("  ".gnodename($n)." ");
        my @s;

        if (defined $n->{Result}) {
            if ($n->{Result} eq 'fail') {
                push @s, 'fillcolor="red"';
                push @s, 'fontcolor="white"';
            } elsif ($n->{Result} eq 'pass') {
                push @s, 'fillcolor="#00c000"';
                push @s, 'fontcolor="black"';
            } else {
                push @s, 'fillcolor="blue"';
                push @s, ' fontcolor="white"';
            }
        } elsif (defined $n->{Conflicted}) {
            push @s, ' fillcolor="yellow"';
            push @s, ' fontcolor="black"';
        } else {
        }

        if ($n->{Target}) {
            push @s, ' peripheries=2';
            push @s, ' style="bold,filled"';
        }

        odot('[');
        odot(join ', ', @s);
        odot("];\n");
        

        foreach my $parent (@{ $n->{Parents} }) {
            odot("  ".gnodename($parent)." -> ".gnodename($n).";\n");
        }
    }
    odot("}\n");

    close DOT or die $!;

    foreach my $fmt (qw(ps png)) {
        system_checked("dot -T$fmt -o$graphfile.$fmt $graphfile.dot");
    }
    system_checked("convert -resize $c{BisectionRevisonGraphSize}".
                   " $graphfile.png $graphfile.mini.png");
    my $href= $graphfile;
    $href =~ s,.*/,,;
    open HTML, "> $graphfile.html" or die "$graphfile.html $!";
    print HTML <<END
<html><head><title>bisection $branch $job $testid</title></head>
<body>
<h1>$branch $job $testid</h1>
<h2>Trees</h2>
<pre>
END
    . (join '', map { "  $_\n" } @trees). <<END
</pre>
<h2>Revision graph overview</h2>
<img src="$href.mini.png">
<h2>Revision graph</h2>
<a href="$href.ps">PostScript</a><p>
<img src="$href.png">
</body></html>
END
        or die "$graphfile.html $!";

    print STDERR "Revision graph left in $graphfile.{dot,ps,png,html}.\n";
}

our %jobs_created;

sub preparejob ($$$);
sub preparejob ($$$) {
    my ($popjob, $copyflight, $cacheok) = @_;

    if (exists $jobs_created{$popjob}) {
	print STDERR "Already created $jobs_created{$popjob}\n";
	return $jobs_created{$popjob};
    }

    print STDERR "Need $popflight.$popjob (like $copyflight)\n";

    # Create a temporary table containing the runvars we want

    $dbh_tests->do(<<END, {}, $copyflight, $popjob);
        SELECT name, val
            INTO TEMP  bisection_runvars
                 FROM  runvars
                WHERE  flight=? AND job=? AND synth='f'
                  AND  name NOT LIKE E'revision\\_%'
END
    my (@trevisions) = split / /, $choose->{Rtuple};

    my $treeq= $dbh_tests->prepare(<<END);
        SELECT name FROM runvars
          WHERE  flight=? AND job=?
            AND  name LIKE E'tree\\_%'
            AND  val = ?
END
    foreach (my $i=0; $i<@trees; $i++) {
        $treeq->execute($copyflight, $popjob, $trees[$i]);
        my ($treevar) = $treeq->fetchrow_array();
        next unless defined $treevar;
        $treevar =~ m/^tree_/ or
            die "$copyflight $popjob $trees[$i] $treevar ";
        my $revname= "revision_$'";
        my $revval= $trevisions[$i];

        print STDERR " $revname=$revval\n";
        $dbh_tests->do(<<END, {}, $revname, $revval);
            INSERT INTO bisection_runvars VALUES (?, ?)
END
    }

    # Check for subjobs:

    my $jobq= $dbh_tests->prepare(<<END);
        SELECT name, val FROM runvars
            WHERE  flight=? AND job=?
              AND  name LIKE '%job';
END
    $jobq->execute($copyflight, $popjob);
    my $subjobs= $jobq->fetchall_arrayref( {} );

    # See if there's a build we can reuse

    my $recipeq= $dbh_tests->prepare(<<END);
        SELECT recipe FROM jobs WHERE flight=? AND job=?
END
    $recipeq->execute($copyflight,$popjob);
    my ($recipe) = $recipeq->fetchrow_array();

    my $usejob;

    if ($cacheok and $recipe =~ m/^build/ and !@$subjobs) {
        my $reuseq= $dbh_tests->prepare(<<END);
            SELECT  *
            FROM    flights f NATURAL JOIN jobs j
            WHERE   j.job=?
            AND     j.status='pass'
            AND     $blessingscond
            AND     0 = ( SELECT count(*)
                          FROM
                              ( SELECT name,val
                                FROM   runvars
                                WHERE  flight=f.flight AND job=? AND synth='f'
                              ) o
                          FULL OUTER JOIN
                              bisection_runvars want
                          USING (name)
                          WHERE (o.val = want.val) IS NOT TRUE
                        )
            ORDER BY started desc
            LIMIT 1
END
        $reuseq->execute($popjob,$popjob);
        my $reusejob= $reuseq->fetchrow_hashref();
        if ($reusejob) {
            $usejob= "$reusejob->{flight}.$reusejob->{job}";
            print STDERR "Can reuse $recipe $usejob.\n";
        }
    }

    if (!defined $usejob) {
        print STDERR "Creating $popflight.$popjob (copying".
            " from $copyflight)\n";

        $dbh_tests->do(<<END, {}, $popflight,$popjob,$recipe);
            INSERT INTO jobs VALUES (?, ?, ?, 'queued')
END
        $dbh_tests->do(<<END, {}, $popflight,$popjob);
            INSERT INTO runvars (
                    SELECT ? as flight, ? as job, name, val, 'f'
                      FROM bisection_runvars
                                 )
END

        $usejob= "$popflight.$popjob";
        $jobs_created{$popjob}= $usejob;
    }

    $dbh_tests->do("DROP TABLE bisection_runvars");

    my $jobsetq= $dbh_tests->prepare(<<END);
        UPDATE runvars SET val=?
            WHERE  flight=? AND job=? AND name=?
END
    foreach my $subjob (@$subjobs) {
        my $target;
        my $jobspec= $subjob->{val};
        if ($jobspec =~ m/^(\d+)\.(\s+)$/) {
            $target= preparejob($1, $2, 1);
        } else {
            $target= preparejob($jobspec, $copyflight, 1);
        }
        $jobsetq->execute($target, $popflight, $popjob, $subjob->{name});
    }

    return $usejob;
}

sub populateflight () {
    return unless $popflight;

    if (!defined $choose) {
        print STDERR "No revision to test.\n";
        return;
    }

    db_retry($dbh_tests, sub {
        print STDERR "Populating $popflight...\n";
        preparejob($job, $latest_flight, 0);
    });

    print STDERR "Flight $popflight ready to go.\n";
}

sub compute_exitstatus () {
    if (defined($choose)) {
        $estatus= 0;
    } else {
        die if $worst_estatus==0;
        $estatus= $worst_estatus;
    }
}

csreadconfig();
findbasics();
digraph_whole();
digraph_relevant();
scantests();
consolidateresults();
search();
writegraph();
populateflight();
compute_exitstatus();
