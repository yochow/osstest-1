#!/usr/bin/tclsh8.4
# -*- Tcl -*- 
# usage: ./ms-ownerdaemon  ... | logger

# This is part of "osstest", an automated testing framework for Xen.
# Copyright (C) 2009-2013 Citrix Inc.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.


source ./tcl/daemonlib.tcl


proc chan-destroy-stuff {chan} {
    upvar #0 chanawait($chan) await
    catch { unset await }

    upvar #0 chantasks($chan) tasks
    if {![info exists tasks]} return

    jobdb::transaction resources {
        puts-chan-desc $chan "-- $tasks"
        foreach task $tasks {
            pg_execute dbh "
                UPDATE tasks
                   SET live = 'f'
                 WHERE taskid = $task
            "
        }
    }
    puts-chan-desc $chan "== $tasks"
    unset tasks

    after idle await-endings-notify
}

proc await-endings-notify {} {
    global chanawait
    foreach chan [array names chanawait] {
        for-chan $chan {
            puts-chan $chan "!OK await-endings notify"
        }
    }
}

proc cmd/create-task {chan desc} {
    upvar #0 chantasks($chan) tasks
    if {[info exists tasks]} {
        set taskdesc "$desc-[llength $tasks]"
    } else {
        set taskdesc $desc
    }
    jobdb::transaction resources {
        pg_execute dbh "
            INSERT INTO tasks
                        ( type,  refkey,               refinfo,         live)
                 VALUES ('ownd', [pg_quote $taskdesc], [clock seconds], 't')
        "
        set nrows [pg_execute -array av dbh "
            SELECT taskid
              FROM tasks
             WHERE live AND refkey = [pg_quote $taskdesc]
        "]
        if {$nrows != 1} { error "multiple $taskdesc!" }
        set task $av(taskid)
    }
    lappend tasks $task
    puts-chan $chan "OK created-task $task ownd $taskdesc"
}

proc cmd/await-endings {chan desc} {
    upvar #0 chanawait($chan) await
    set await 1
    puts-chan $chan "OK await-endings"
}

proc banner {chan} {
    return "OK ms-ownerdaemon"
}

main-daemon Owner {
    jobdb::db-open
    
    jobdb::transaction resources {
        set nrows [pg_execute dbh "
            UPDATE tasks
               SET refkey = 'previous ' || refkey
             WHERE type = 'ownd'
               AND refkey NOT LIKE 'previous %'
        "]
        log "started ($nrows staler tasks)"
    }
}
