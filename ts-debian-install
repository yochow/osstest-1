#!/usr/bin/perl -w
# This is part of "osstest", an automated testing framework for Xen.
# Copyright (C) 2009-2013 Citrix Inc.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict qw(vars);
use DBI;
use Osstest;
use Osstest::TestSupport;

tsreadconfig();

our ($whhost,$gn) = @ARGV;
$whhost ||= 'host';
$gn ||= 'debian';

our $ho= selecthost($whhost);

our $ram_mb=    512;
our $swap_mb=  1000;
our $disk_mb= 10000;

our $guesthost= "$gn.guest.osstest";
our $gho;

sub prep () {
    target_install_packages_norec($ho, qw(lvm2 xen-tools));

    $gho= prepareguest($ho, $gn, $guesthost, 22,
                       $swap_mb + $disk_mb + 2,
                       40);
    target_cmd_root($ho, "umount $gho->{Lvdev} ||:");
}

sub ginstall () {
    my $arch= $r{"$gho->{Guest}_arch"};
    my $archarg= defined($arch) ? "--arch $arch" : '';
    my $gsuite= guest_var($gho,'suite',$c{GuestDebianSuite});

    my $kernpath = guest_var($gho,'kernel_path',$r{xen_kernel_path});
    my $initrd = guest_var($gho,'initrd_path',$r{xen_initrd_path});
    if (!$kernpath) {
	my $kernver= guest_var($gho,'kernel_ver',$r{xen_kernel_ver});
	$kernver ||= target_cmd_output($ho, 'uname -r');
	$kernpath = "/boot/vmlinuz-$kernver";
	$initrd ||= "/boot/initrd.img-$kernver";
    }
    if (!$initrd) {
	$initrd = $kernpath;
	$initrd =~ s,/vmlinuz-,/initrd.img-, or die "$initrd ?";
    }
    my $initrd_opt = $initrd eq 'none' ? '' : "--initrd $initrd";
    
    target_cmd_root($ho, <<END, 2000);
        xen-create-image \\
            --dhcp --mac $gho->{Ether} \\
	    --memory ${ram_mb}M --swap ${swap_mb}M \\
            --dist $gsuite \\
            --mirror http://$c{DebianMirrorHost}/$c{DebianMirrorSubpath} \\
            --hostname $gho->{Name} \\
            --lvm $gho->{Vg} --force \\
            --kernel $kernpath \\
            --genpass 0 --password xenroot \\
            $initrd_opt \\
            $archarg
END
    my $cfg_xend= "/etc/xen/$gho->{Name}.cfg";
    store_runvar("$gho->{Guest}_cfgpath", $cfg_xend);
    store_runvar("$gho->{Guest}_swap_lv", "$gho->{Name}-swap");
}

prep();
ginstall();
