#!/usr/bin/perl -w

use strict qw(vars);
use DBI;
use Osstest;

readconfig();
opendb_state();
our ($ho,$gho) = ts_get_host_guest(@ARGV);

our ($cfgfile,$cfgstash,$cfg);

sub savecfg () {
    $cfgfile= $r{"$gho->{Guest}_cfgpath"};
    $cfgfile =~ m,([^/]+)$, or die "$cfgfile";
    my $cfgleaf= $1;
    $cfgstash= "$stash/$cfgleaf";
    target_getfile($ho,10, $cfgfile, "$cfgstash.orig");
    $cfg= get_filecontents("$cfgstash.orig");
}

sub ether () {
#    $cfg =~ s/^ [ \t]*
#        ( vif [ \t]* \= [ \t]* \[ [ \t]* [\'\"]
#          [^\n\'\"]* \b mac= ) [0-9a-fA-F:]+ \b ( [^\n\'\"]* [\'\"]
#          [^\n\'\"]* )
#          /
#            my $n= $1.$gho->{Ether}.$2;
#            logm("changed vif to $n");
#            $n;
#          /mxe
#        or die "no ether?";
#
    store_runvar("$gho->{Guest}_tcpcheckport", 22);
}

sub access () {
    guest_umount_lv($ho, $gho);
    target_cmd_root($ho, <<END);
        set -ex
        mount /dev/$gho->{Vg}/$gho->{Lv} /mnt
        perl -i~ -pe "s/^root:[^:]+:/root::/" /etc/shadow
        mkdir -p /mnt/root/.ssh /mnt/etc/ssh
        cp -a /root/.ssh/* /mnt/root/.ssh/.
        cp -a /etc/ssh/ssh_host_*key* /mnt/etc/ssh/.
END
}

our $extra;

sub console () {
    my $console=
        target_kernkind_console_inittab($ho,$gho,"/mnt");
    return unless length $console;
    
    my $xextra= "console=$console earlyprintk=xen";
    my $orgextra;
    if ($cfg =~ s/^(extra=[ \t]*[\'\"](.*)[\'\"][ \t]*)\n/\n/m) {
        $orgextra= $2;
        logm("extra was: $1");
        $orgextra =~ s/\bconsole=\S+\s*//g;
        $orgextra.= ' ';
    } elsif ($cfg =~ m/^extra\b.*/) {
        die "$& ?";
    } else {
        $orgextra= '';
    }
    $orgextra =~ s/\bearlyprintk=\S+\s*//g;
    $extra= "$orgextra$xextra";
    logm("extra: $extra");
}

sub filesystems () {
    my $rootdev= $r{"$gho->{Guest}_rootdev"};
    return unless defined($rootdev) && length($rootdev);
    logm("adjusting root: $rootdev");
    $cfg =~ s,^(root \s* = \s* ['"] /dev/)sda(\d+ \s .*)$, $1.$rootdev.$2 ,mxe;
    $cfg =~ s#('phy: [^,: \t]+ , )sda(\d+ ,w')# $1.$rootdev.$2 #gmxe;

    target_cmd_root($ho, <<END);
        set -ex
        perl -i~ -pe "
            s,^(/dev/)sda(\\\\d+),\\\${1}$rootdev\\\$2,;
        " /mnt/etc/fstab
END
}

sub unmount () {
    guest_umount_lv($ho, $gho);
}

sub setcfg ($$) {
    my ($lhs,$rhs) = @_;
    $cfg =~ s/^$lhs\s*\=.*\n?//mg;
    $cfg .= "$lhs = $rhs\n";
}

sub otherfixupcfg () {
    my $vcpus= guest_var($gho,'vcpus',1);
    $cfg =~ s/^dhcp/#$&/mg;
    $cfg =~ s/^on_crash.*/on_crash='preserve'/mg;
    $cfg =~ s/^vcpus.*//mg;
    $cfg .= "\nvcpus = $vcpus\n";

    $cfg .= "\nextra='$extra'\n";
};

sub writecfg () {
    my $h= new IO::File $cfgstash, 'w' or die "$cfgstash $!";
    print $h $cfg or die $!;
    close $h or die $!;
    target_putfile_root($ho,10, $cfgstash, $cfgfile);
}

sub xlcfg () {
    my $cfg_xend= target_var($gho,'cfgpath');
    my $cfg_xl= "/etc/xen/$gho->{Name}.xl";
    store_runvar("$gho->{Guest}_xlpath", $cfg_xl);
    target_editfile_root($ho, $cfg_xend, undef, $cfg_xl, sub {
        local ($/)= undef;
        $_= <EI>;
        s/^ [ \t]* memory [ \t]* \= [ \t]* ['"] (.+) ['"] [ \t]*
          $/memory=$1/mx;
        s/([[,])\s*\n/$1/g;
        s/,\s*\]/\]/g;
        s/([^#\n].*)\n/$1;\n/mg;
        s/\n\n+/\n/g;
        print EO or die $!;
    });
}

savecfg();
ether();
target_kernkind_check($gho);
access();
console();
filesystems();
otherfixupcfg();
writecfg();
xlcfg();
unmount();
