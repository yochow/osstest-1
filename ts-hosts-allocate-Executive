#!/usr/bin/perl -w
# This is part of "osstest", an automated testing framework for Xen.
# Copyright (C) 2009-2013 Citrix Inc.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;
use DBI;
use Osstest;
use Data::Dumper;
use POSIX;
use JSON;
use IO::Handle;
use Osstest::TestSupport;
use Osstest::Executive;

tsreadconfig();

open DEBUG, ">/dev/null" or die $!;

while (@ARGV and $ARGV[0] =~ m/^-/) {
    $_= shift @ARGV;
    last if m/^--$/;
    while (m/^-./) {
        if (0) {
	    # no options
        } else {
            die "$_ ?";
        }
    }
}

# initialised by setup:
our $taskid;
our %magictaskid;
our $fi;
our $jobinfo;
our $harness_rev;

#---------- general utilities, setup, etc. ----------

sub show_reskey {
    my (@reskey) = @_;
    return $reskey[2] eq '0' ? "@reskey[0..1]" : "@reskey";
}

sub setup () {
    $harness_rev = get_harness_rev();

    $taskid= findtask();

    my $logbase = "hosts-allocate.debug.gz";
    my $logfh = open_unique_stashfile \$logbase;
    my $logchild = open DEBUG, "|-";  defined $logchild or die $!;
    if (!$logchild) {
	open STDOUT, ">&", $logfh or die $!;
	exec "gzip" or die $!;
    }
    DEBUG->autoflush(1);
    logm("host allocation debug log in $logbase");
    $logm_handle = [ $logm_handle, \*DEBUG ];

    $fi= $dbh_tests->selectrow_hashref(<<END, {}, $flight);
        SELECT * FROM flights
         WHERE flight = ?
END
    logm("flight $flight intended $fi->{intended} branch $fi->{branch}");

    $jobinfo= $dbh_tests->selectrow_hashref(<<END, {}, $flight, $job);
        SELECT * FROM jobs
         WHERE flight = ? AND job = ?
END
    logm("job $flight.$job recipe $jobinfo->{recipe}");

    foreach my $rk (qw(allocatable shared preparing idle)) {
        $magictaskid{$rk}= $dbh_tests->selectrow_array(<<END, {}, $rk);
            SELECT taskid FROM tasks
             WHERE type='magic' AND refkey=?
END
    }
}

#---------- prepared sql statements ----------
# all users of these must ->finish them afterwards, to avoid db deadlock

our ($flagscheckq, $equivflagscheckq, $duration_estimator, $resprop_q,
     $alloc_findres_q, $alloc_shared_q, $alloc_sharing_slot_q,
     $claim_share_reuse_q, $claim_maxshare_q, $claim_rmshares_q,
     $claim_noshares_q, $claim_rmshare_q, $claim_setres_q,
     $claim_share_new_q, $claim_share_newresource_q);
    
sub prepare_statements () {
    $flagscheckq= $dbh_tests->prepare(<<END);
	    SELECT * FROM hostflags
	     WHERE hostname = ? AND hostflag = ?
END

    $equivflagscheckq= $dbh_tests->prepare(<<END);
	    SELECT * FROM hostflags
		    WHERE hostname = ?
		      AND hostflag LIKE 'equiv-%'
END

    $duration_estimator= duration_estimator($fi->{branch}, $fi->{intended},
                                            sub { print DEBUG "@_\n"; });

    $resprop_q= $dbh_tests->prepare(<<END);
            SELECT * FROM resource_properties
                    WHERE restype = ? AND resname = ?
                      AND name = ?
END

    # for allocation

    $alloc_findres_q= $dbh_tests->prepare(<<END);
	    SELECT *
	      FROM resources
	     WHERE restype=? AND resname=? AND shareix=0
END

    $alloc_shared_q= $dbh_tests->prepare(<<END);
            SELECT s.restype, s.resname, s.sharetype, s.state,
                   s.wear, s.harness,
                   ( SELECT count(*)
		       FROM resources r
	              WHERE r.restype=?
                        AND r.resname=?
                        AND r.owntaskid!=?
                        AND r.owntaskid!=?
                     ) AS ntasks
              FROM resource_sharing s
             WHERE s.restype=? AND s.resname=?
END
    # s.. # fix perl-mode

    $alloc_sharing_slot_q= $dbh_tests->prepare(<<END);
            SELECT * FROM resources
	            WHERE restype=? AND resname=? AND owntaskid=?
		    LIMIT 1
END

    $claim_share_reuse_q= $dbh_tests->prepare(<<END);
        UPDATE resource_sharing
           SET wear = wear + 1
         WHERE restype=? AND resname=?
END

    $claim_maxshare_q= $dbh_tests->prepare(<<END);
        SELECT max(shareix) AS shares
          FROM resources WHERE restype=? AND resname=?
END

    $claim_rmshares_q= $dbh_tests->prepare(<<END);
        DELETE FROM resources
              WHERE restype=? AND resname=?
                AND (owntaskid=? OR owntaskid=?)
END

    $claim_noshares_q= $dbh_tests->prepare(<<END);
        SELECT * FROM resources
              WHERE restype=? AND resname=?
              LIMIT 1
END

    $claim_rmshare_q= $dbh_tests->prepare(<<END);
        DELETE FROM resource_sharing
              WHERE restype=? AND resname=?
END

    $claim_setres_q= $dbh_tests->prepare(<<END);
        UPDATE resources
           SET owntaskid = ?, subtask = ?
         WHERE restype=? AND resname=? AND shareix=?
END
    $claim_share_new_q= $dbh_tests->prepare(<<END);
        INSERT INTO resource_sharing
                    (restype, resname, sharetype, state,  wear, harness)
             VALUES (?,       ?,       ?,         'prep', 1,    ?      )
END
    $claim_share_newresource_q= $dbh_tests->prepare(<<END);
        INSERT INTO resources
                    (restype, resname, shareix, owntaskid, subtask)
             VALUES (?,       ?,       ?,       ?,         ''     )
END

}

#---------- finding possibilites for a particular host ----------

our @hids;

sub compute_hids () {
    our %equivs;

    foreach my $ident (@ARGV) {
        my $hid= { };
        my $override_use;
        if ($ident =~ m/\=/) {
            $hid->{OverrideUse}= $'; #'
            $ident= $`;
            print DEBUG "HID $ident OVERRIDE $hid->{OverrideUse}\n";
        }
        my @flags= get_hostflags($ident);
	die "no hostflags?!" unless @flags;
        print DEBUG "HID $ident FLAGS @flags\n";
        $hid->{Ident}= $ident;
        my %flags;
        foreach my $flag (@flags) {
            print DEBUG "HID $ident FLAG $flag\n";
            if ($flag =~ m/^share-/) {
                die if exists $hid->{Shared};
                my $shr= $'; #'
                $hid->{Shared}= $shr." ".get_harness_rev();
                if ($shr =~ m/^build-/) {
                    $hid->{DefaultSharedMaxTasks}= 3;
                    $hid->{DefaultSharedMaxWear}= 10;
                } else {
                    # who can say
                    $hid->{DefaultSharedMaxTasks}= 2;
                    $hid->{DefaultSharedMaxWear}= 5;
                }
                print DEBUG "HID $ident FLAG $flag SHARE $shr".
                    " $hid->{DefaultSharedMaxTasks}".
                    " $hid->{DefaultSharedMaxWear}\n";
                next;
            } elsif ($flag =~ m/^equiv-/) {
                my $formalclass= $'; #'
                die if exists $hid->{Equiv};
                $equivs{$formalclass}{FormalClass}= $formalclass;
                $equivs{$formalclass}{Wanted}++;
                my $equiv= $hid->{Equiv}= $equivs{$formalclass};
                print DEBUG "HID $ident FLAG $flag EQUIV $equiv->{Wanted}\n";
                next;
            }
            $flags{$flag}= 1;
        }
        $hid->{Flags}= \%flags;
        print DEBUG "HID $ident FLAGS ".(join ',', sort keys %flags)."\n";
        push @hids, $hid;
    }
}

sub hid_find_possibilities ($) {
    my ($hid) = @_;

    delete $hid->{Candidates};

    my $use= $hid->{OverrideUse} || $r{ $hid->{Ident} };

    my $findhostsq;
    if (defined $use) {
        print DEBUG "HID $hid->{Ident} USE $use\n";
	$findhostsq= $dbh_tests->prepare(<<END);
	    SELECT *
	      FROM resources
	     WHERE restype=? AND resname=? AND shareix=0
END
        $findhostsq->execute('host',$use);
    } else {
        print DEBUG "HID $hid->{Ident} INTENDED $fi->{intended}\n";
	$findhostsq= $dbh_tests->prepare(<<END);
	    SELECT *
	      FROM resources JOIN hostflags
		ON (restype='host' AND shareix=0 AND
		    resname=hostname AND hostflag=?)
END
        $findhostsq->execute("blessed-$fi->{intended}");
    }

    my @candidates;
    my $any=0;

    while (my $candrow= $findhostsq->fetchrow_hashref()) {
        $candrow->{Warnings}= [ ];
        $candrow->{Reso}= "$candrow->{restype} $candrow->{resname}";

        my $dbg= "HID $hid->{Ident} TRY $candrow->{Reso}:";
        print DEBUG "$dbg\n";
        my @missingflags;

        my $needflag= sub {
            my ($flag) = @_;
            print DEBUG "$dbg NEEDFLAG $flag\n";
            $flagscheckq->execute($candrow->{resname}, $flag);
            my $row= $flagscheckq->fetchrow_arrayref();
            $flagscheckq->finish();
            return if $row;
            push @missingflags, $flag;
        };

        foreach my $flag (keys %{ $hid->{Flags} }) {
            $needflag->($flag);
        }
        my $equiv= $hid->{Equiv};
        if ($equiv && !defined $use) {
            # if it was specified exactly we don't care about equiv classes
            # if you specify one of a class you should specify all, then!
            print DEBUG "$dbg EQUIV $equiv->{FormalClass}\n";
	    $equivflagscheckq->execute($candrow->{resname});
	    my $erow= $equivflagscheckq->fetchrow_hashref();
	    if (!$erow) {
		print DEBUG "$dbg EQUIV $equiv->{FormalClass} NO-CLASSES\n";
		next;
	    }
	    my $eq= $erow->{hostflag};
	    print DEBUG "$dbg EQUIV $equiv->{FormalClass} MAYBE $eq\n";
	    $candrow->{EquivActual}= $eq;
	    $erow= $equivflagscheckq->fetchrow_hashref();
	    if ($erow) {
		push @{ $candrow->{Warnings} },
		    "host has multiple equivalence class flags";
	    }
	    $equivflagscheckq->finish();
	}

        print DEBUG "$dbg FLAGS MISSINGFLAGS: @missingflags.\n";
        if (@missingflags) {
            next unless defined $use;
            push @{ $candrow->{Warnings} },
                "specified host lacks flags @missingflags";
        }
        $any++;

        print DEBUG "$dbg GOOD\n";

        find_recent_duration($dbg,$hid,$candrow);

        foreach my $kcomb (qw(Shared-Max-Wear Shared-Max-Tasks)) {
            my $kdb= $kcomb;  $kdb =~ y/-A-Z/ a-z/;
            my $khash= $kcomb;  $khash =~ y/-//d;
            $resprop_q->execute($candrow->{restype},$candrow->{resname},$kdb);
            my $proprow= $resprop_q->fetchrow_hashref();
            my $val= $proprow->{val};
            if (defined $val) {
                print DEBUG "$dbg $khash ($kdb) FROM-RES-PROP $val\n";
            } else {
                $val= $hid->{"Default$khash"};
                print DEBUG "$dbg $khash ($kdb) FROM-DEFAULT ".
		    visible_undef($val)."\n";
            }
            $candrow->{$khash}= $val;
        }

	push @candidates, $candrow;
	print DEBUG "$dbg CANDIDATE.\n";
    }
    $findhostsq->finish();

    if (!@candidates) {
        if (defined $use) {
            logm("specified host $use for $hid->{Ident} nonexistent?");
        } else {
            logm("no suitable host for $hid->{Ident} (out of $any)");
        }
    }

    $hid->{Candidates} = \@candidates;
}

sub find_recent_duration ($$) {
    my ($dbg, $hid, $candrow) = @_;
    
    ($candrow->{Duration},
     $candrow->{MostRecentStarted},
     $candrow->{MostRecentStatus}) =
        $duration_estimator->($job, $hid->{Ident}, $candrow->{resname});
}


#---------- constructing a plan ----------

sub hid_class_size {
    my ($hid) = @_;
    my $equiv= $hid->{Equiv};
    return 0 unless $equiv;
    return $equiv->{Wanted};
}

sub optimally_order_hids () {
    @hids= sort {
	hid_class_size($b) <=> hid_class_size($a) ||
	@{ $a->{Candidates} } <=> @{ $b->{Candidates} }
    } @hids;
    print DEBUG "ORDER ".(join ' ', map { $_->{Ident} } @hids)."\n";
}

# reset each time around:
our $plan;
our $best;

sub hid_recurse ($$);
sub hid_recurse ($$) {
    my ($actualmap, $hidix) = @_;
    # fills in the plan from $hidix (inclusive) to the end

    our %noreuse;
    my $dbg= sprintf "RECURSE %*s", $hidix*2, '';
    if ($hidix < @hids) {
	my $hid= $hids[$hidix];
	print DEBUG "$dbg $hid->{Ident}...\n";
	foreach my $cand (@{ $hid->{Candidates} }) {
            my $reso= $cand->{Reso};
	    print DEBUG "$dbg CAND $reso\n";
            next if defined $noreuse{$reso};

	    my $poss_actual= $cand->{EquivActual};
	    my $newactualmap= $actualmap;
	    if (defined $poss_actual) { # i.e. iff $hid->{Equiv} and not $use
		my $equiv= $hid->{Equiv};
		my $formal= $equiv->{FormalClass};
		my $already= $actualmap->{$formal};
		if (defined $already) {
		    if ($already ne $poss_actual) {
			print DEBUG "$dbg CAND $reso EQUIV".
			    " WRONG $formal: $already != $poss_actual\n";
			next;
		    }
		} else {
		    print DEBUG "$dbg CAND $reso EQUIV".
			" SET $formal: $poss_actual\n";
		    $newactualmap= { %$actualmap, $formal => $poss_actual };
		}
	    }

	    $hid->{Selected}= $cand;
            $noreuse{$reso}=1;
	    hid_recurse($newactualmap, $hidix+1);
            delete $noreuse{$reso};
	}
	return;
    }

    # hah, got to the end, see when we could do it and calculate the cost

    print DEBUG "$dbg EVAL "
        .(join '; ', map { $_->{Selected}{Reso} } @hids)."\n";

    my $variation_age= 0;
    my $duration= undef;
    my $previously_failed = 0;
    foreach my $hid (@hids) {
	my $cand= $hid->{Selected};
	my $recentstarted= $cand->{MostRecentStarted};
	$variation_age +=
            defined $recentstarted ? time - $recentstarted : 90*86400;
	$duration= $cand->{Duration} if
	    !defined($duration) ||
	    defined($cand->{Duration}) && $cand->{Duration} >= $duration;
        $previously_failed++ if
	    defined $cand->{MostRecentStatus} &&
	    $cand->{MostRecentStatus} eq 'fail';
    }
    my $duration_rightaway_adjust= 0;
    
    if (!defined $duration) {
        # if we can start now then don't penalise unknown hosts so much
        $duration= 5000;
        $duration_rightaway_adjust= 1000 - $duration;
    }
    $duration += 10;

    print DEBUG "$dbg EVAL DURATION $duration va=$variation_age\n";

    if ($jobinfo->{recipe} =~ m/build/) {
        $variation_age= 0;
    } elsif ($variation_age > 5*86400) {
	$variation_age= 5*86400;
    }	

    my @requestlist;
    foreach my $hid (@hids) {
        my $req= {
            Reso => $hid->{Selected}{Reso},
            Ident => $hid->{Ident},
            Shared => $hid->{Shared},
            SharedMaxWear => $hid->{Selected}{SharedMaxWear},
            SharedMaxTasks => $hid->{Selected}{SharedMaxTasks},
        };
        push @requestlist, $req;
    }
    my $dbgprint= sub { print DEBUG "$dbg @_\n"; };
    my $planned= plan_search($plan, $dbgprint, $duration, \@requestlist);
    my $start_time= $planned->{Start};
    my $share_reuse= $planned->{ShareReuse};

    $duration_rightaway_adjust=0 if $start_time;

    my $cost= $start_time
	+ $duration
	+ $duration_rightaway_adjust
        - $previously_failed * 366*86400
        + ($previously_failed ? + $variation_age * 10 : - $variation_age / 30)
	- $share_reuse * 10000;
    
    print DEBUG "$dbg FINAL start=$start_time va=$variation_age".
        " previously_failed=$previously_failed cost=$cost\n";

    if (!defined $best || $cost < $best->{Cost}) {
        print DEBUG "$dbg FINAL BEST: ".
	    (join '; ', map { $_->{Selected}{Reso} } @hids). "\n";
	$best= {
	    Cost => $cost,
	    Selections => [ map { $_->{Selected} } @hids ],
	    Start => $start_time,
	    Duration => $duration,
	};
    }
}

#---------- committing to a plan ----------

sub alloc_hosts () {
    if (!@hids) {
        logm("host allocation: no nosts requested!");
        return;
    }

    my $waitstartadjust=
        $jobinfo->{recipe} =~ m/build/
        ? -10000
        : -10 * @hids;

    alloc_resources(WaitStart =>
                    ($ENV{OSSTEST_RESOURCE_WAITSTART} || $fi->{started}),
                    WaitStartAdjust => $waitstartadjust,
		    DebugFh => \*DEBUG,
                    \&attempt_allocation);

    foreach my $hid (@hids) {
        my $sel= $hid->{Selected};
        die $hid->{Ident} unless defined $sel->{resname};
        my $use= $r{ $hid->{Ident} };
        next if defined $use;
        store_runvar($hid->{Ident}, $sel->{resname});
    }

    logm("host allocation: all successful and recorded.");
}

sub attempt_allocation {
    ($plan) = @_;
    undef $best;

    logm("allocating hosts: ".join(' ', map { $_->{Ident} } @hids));

    prepare_statements();

    foreach my $hid (@hids) {
	delete $hid->{Allocated};
	hid_find_possibilities($hid);
    }
    optimally_order_hids();

    hid_recurse({}, 0);

    if (!$best) {
	logm("no plan is possible");
	die "no plan is possible";
    }

    foreach (my $ix=0; $ix<@hids; $ix++) {
	$hids[$ix]{Selected}= $best->{Selections}[$ix];
    }

    my $retval=0;

    if (!$best->{Start}) {
	$retval= 1;
	foreach my $hid (@hids) {
	    my $got= actual_allocation($hid);
	    if (!$got) {
		$retval= 0;
		last;
	    }
	    $hid->{Allocated}= $got;
	}
    }
    if (!$retval) {
	foreach my $hid (@hids) { delete $hid->{Allocated}; }
    }

    if ($retval) {
	logm("host allocation: successful, reporting to planner.");
    } else {
	logm("host allocation: planned start in $best->{Start} seconds.");
    }

    my $booklist= compute_booking_list();

    return ($retval, $booklist);
}

sub compute_booking_list () {
    my @bookings;
    foreach my $hid (@hids) {
	my $sel= $hid->{Selected};
	my $alloc= $hid->{Allocated};
	my $book= $alloc || {
	    (defined $hid->{Shared} ? (Share => {
		Type => $hid->{Shared},
		Shares => $sel->{SharedMaxTasks},
	    }) : ()),
	};
	$book->{Reso}= "$sel->{restype} $sel->{resname}";
	$book->{Xinfo}= $hid->{Ident};
	$book->{Start}= $best->{Start},
	$book->{End}= $best->{Start} + $best->{Duration},
	push @bookings, $book;
    }
    return { Bookings => \@bookings };
}

#---------- actually allocate things ----------

sub actual_allocation ($) {
    my ($hid) = @_;

    # if successful returns { Allocated =>, Share => }
    # as for booking list otherwise undef

    my $sel= $hid->{Selected};
    my $shr= $hid->{Shared};

    die unless $sel->{shareix}==0;

    $alloc_findres_q->execute($sel->{restype}, $sel->{resname});
    my $cand= $alloc_findres_q->fetchrow_hashref();
    $alloc_findres_q->finish();
    if (!$cand) {
        warn "resource $sel->{Reso} allegedly allocatable but not found";
        return undef;
    }

    my $allocatable= $cand->{owntaskid} == $magictaskid{allocatable};
    my $dbg= "$hid->{Ident} $sel->{Reso}";
    printf DEBUG "%s ALLOC PLAIN %d\n", $dbg, $allocatable;

    my $shared;
    my $shrestype= 'share-'.$sel->{restype};

    if ($cand->{owntaskid} == $magictaskid{shared}) {
	$alloc_shared_q->execute
                ($shrestype, $sel->{resname},
                 $magictaskid{allocatable}, $magictaskid{preparing},
                 $sel->{restype}, $sel->{resname});
	$shared= $alloc_shared_q->fetchrow_hashref();
	$alloc_shared_q->finish();

	if (!$shared) {
            warn "resource $sel->{Reso} allegedly shared but no sharing info";
            return undef;
        }

	print DEBUG "$dbg ALLOC ISSHARED".
	    " $shared->{sharetype} $shared->{state}".
	    " $shared->{ntasks} $shared->{wear} $shared->{harness}\n";

	if (!$shared->{ntasks}) {
	    print DEBUG "$dbg ALLOC ISSHARED CAN-UNSHARE\n";
	    $allocatable= 1;
        }
    }

    my @allocwarnings;

    if (defined $shr &&
	defined $shared &&
	$shared->{sharetype} eq $shr &&
	$shared->{wear} < $sel->{SharedMaxWear} &&
        $shared->{harness} eq $harness_rev) {

	print DEBUG "$dbg ALLOC SHARED ST $shrestype $shared->{state}\n";

	if ($shared->{state} eq 'ready') {
	    $alloc_sharing_slot_q->execute($shrestype, $shared->{resname},
					   $magictaskid{allocatable});
	    my $slot= $alloc_sharing_slot_q->fetchrow_hashref();
	    $alloc_sharing_slot_q->finish();
	    if (!$slot) {
		# this is no use then
		print DEBUG "$dbg ALLOC SHARED AVAIL BUSY\n";
		warn "resource $shrestype $shared->{resname} allegedly".
                    " shareable but no slots";
		return undef;
	    } else {
		print DEBUG "$dbg ALLOC SHARED AVAIL SLOTFREE".
		    " $slot->{shareix}\n";
		$allocatable= 2;
		$cand= $slot;
	    }
	} else {
	    if ($shared->{ntasks}) {
		warn "resource $shrestype $shared->{resname} allegedly".
                    " available but wrong state $shared->{state} and tasks";
		return undef;

                # someone was preparing it but they aren't any more
                push @allocwarnings,
                    "previous preparation apparently abandoned";
                $allocatable= 1;
            }
        }
    }

    if (!$allocatable) {
	warn "resource $sel->{restype} $sel->{resname} allegedly".
            " allocatable but not really";
	return undef;
    }

    # Right, allocate this one!
    my @reskey= map { $cand->{$_} } qw(restype resname shareix);
    print DEBUG "HID $hid->{Ident} ALLOC @reskey\n";

    my $bookalloc= { };

    if ($allocatable==2) {
	# sharing reuse
        print DEBUG "HID $hid->{Ident} GO @reskey SHARING REUSE $shrestype\n";

        $claim_share_reuse_q->execute($sel->{restype}, $cand->{resname});
        $claim_share_reuse_q->finish();

	$claim_maxshare_q->execute($shrestype, $cand->{resname});
	my ($nshares) = $claim_maxshare_q->fetchrow_array();
	$claim_maxshare_q->finish();

	$bookalloc->{Share}= {
	    Type => $shr,
	    Shares => $nshares,
	};
    } else {
        if ($shared) {
            print DEBUG "HID $hid->{Ident} GO @reskey UNSHARE\n";
            $claim_rmshares_q->execute($shrestype, $cand->{resname},
                   $magictaskid{allocatable}, $magictaskid{preparing});
            $claim_rmshares_q->finish();

            $claim_noshares_q->execute($shrestype, $cand->{resname});
            my $bad= $claim_noshares_q->fetchrow_hashref();
            $claim_noshares_q->finish();
            die Dumper($bad).'?' if $bad;

            $claim_rmshare_q->execute($cand->{restype},$cand->{resname});
            $claim_rmshare_q->finish();
        }
        if ($shr) {
            print DEBUG "HID $hid->{Ident} GO @reskey SHARE\n";
            $claim_setres_q->execute($magictaskid{shared},"",
				     $cand->{restype},$cand->{resname},0);
            $claim_setres_q->finish();

            $claim_share_new_q->execute($cand->{restype},$cand->{resname},
					$shr, $harness_rev);
            $claim_share_new_q->finish();

            for (my $ix=1; $ix<=$sel->{SharedMaxTasks}; $ix++) {
                $claim_share_newresource_q->execute
                    ($shrestype, $cand->{resname}, $ix,
                     ($ix==1 ? $magictaskid{allocatable}
                      : $magictaskid{preparing}));
                $claim_share_newresource_q->finish();
            }
            $reskey[0]= $shrestype;
            $reskey[2]= 1;

	    $bookalloc->{Share}= {
		Type => $shr,
		Shares => $sel->{SharedMaxTasks},
	    };
        }
    }
    
    logm("allocating for $hid->{Ident}: ".show_reskey(@reskey));
    foreach my $warn (@allocwarnings, @{ $cand->{Warnings} }) {
        logm("warning about ".show_reskey(@reskey).": $warn");
    }

    print DEBUG "HID $hid->{Ident} GO @reskey ALLOCATE\n";
    my $allocd= $claim_setres_q->execute
        ($taskid, "$flight.$job $hid->{Ident}", @reskey);
    die unless $allocd;
    $claim_setres_q->finish();

    $bookalloc->{Allocated}= { Shareix => $reskey[2], Task => $taskid };
    return $bookalloc;
}

#---------- main program ----------

setup();
compute_hids();
alloc_hosts();
