#!/usr/bin/perl -w

use strict qw(vars);
use DBI;
use Osstest;

use Data::Dumper;

readconfig();
opendb_state();

open DEBUG, ">/dev/null" or die $!;

while (@ARGV and $ARGV[0] =~ m/^-/) {
    $_= shift @ARGV;
    last if m/^--$/;
    while (m/^-./) {
        if (s/^-D/-/) {
            open DEBUG, ">&STDERR" or die $!;
        } else {
            die "$_ ?";
        }
    }
}

our @idents= @ARGV;
our $taskid;

sub alloc_hosts () {

    my $fi= $dbh_tests->selectrow_hashref(<<END, {}, $flight);
        SELECT * FROM flights
         WHERE flight = ?
END

    my %magictaskid;
    foreach my $rk (qw(allocatable shared)) {
        $magictaskid{$rk}= $dbh_tests->selectrow_array(<<END, {}, $rk);
            SELECT taskid FROM tasks
             WHERE type='magic' AND refkey=?
END
    }

    my $hostsq= $dbh_tests->prepare(<<END);
        SELECT *
          FROM resources JOIN hostflags
            ON (restype='host' AND resname=hostname AND hostflag=?)
END

    my $host1q= $dbh_tests->prepare(<<END);
        SELECT *
          FROM resources
         WHERE restype='host' AND resname=?
END

    my $claim_setres_q= $dbh_tests->prepare(<<END);
        UPDATE resources
           SET owntaskid = ?
         WHERE restype='host' AND resname=?
END

    my $claim_share_reuse_q= $dbh_tests->prepare(<<END);
        UPDATE host_sharing
           SET wear = wear + 1
         WHERE hostname = ?
END

    my $claim_share_new_q= $dbh_tests->prepare(<<END);
        INSERT INTO host_sharing
                    (hostname, sharetype, state,  wear)
             VALUES (?,        ?,         'prep', 1   )
END

    my $claim_share_addtask_q= $dbh_tests->prepare(<<END);
        INSERT INTO host_sharing_tasks (owntaskid, hostname, subtask)
             VALUES (?,?,?)
END

    my $claim_unshare_q= $dbh_tests->prepare(<<END);
        DELETE FROM host_sharing
              WHERE hostname = ?
END

    my @hids;
    my %allflags;

    # greedy allocator, but we sort by flags wanted so
    # at least we don't do things obviously stupidly
    foreach my $ident (@idents) {
        my @flags= get_hostflags($ident);
        print DEBUG "HID $ident FLAGS @flags\n";
        my $hid= { Ident => $ident };
        my %flags;
        foreach my $flag (@flags) {
            print DEBUG "HID $ident FLAG $flag\n";
            if ($flag =~ m/^share-/) {
                die if exists $hid->{Shared};
                my $shr= $';
                $hid->{Shared}= $shr;
                if ($shr =~ m/^build-/) {
                    $hid->{SharedMaxTasks}= 3;
                    $hid->{SharedMaxWear}= 10;
                } else {
                    # who can say
                    $hid->{SharedMaxTasks}= 2;
                    $hid->{SharedMaxWear}= 5;
                }
                print DEBUG "HID $ident FLAG $flag SHARE $shr".
                    " $hid->{SharedMaxTasks} $hid->{SharedMaxWear}\n";
                next;
            }
            $flags{$flag}= $allflags{$flag}= 1;
        }
        $hid->{Flags}= \%flags;
        print DEBUG "HID $ident FLAGS ".(join ',', sort keys %flags)."\n";
        push @hids, $hid;
    }

    my $flagscountq= $dbh_tests->prepare(<<END);
        SELECT count(*)
          FROM hostflags
         WHERE hostflag = ?
END
    foreach my $f (keys %allflags) {
        $flagscountq->execute($f);
        my $row= $flagscountq->fetchrow_arrayref();
        die unless defined $row;
        print DEBUG "FLAG $f $row->[0]\n";
        $allflags{$f}= $row->[0];
    }
    my @allflags = sort { $allflags{$a} <=> $allflags{$b} } keys %allflags;

    print DEBUG "FLAGS ALL @allflags\n";

    foreach my $hid (@hids) {
        $hid->{Priority}= join '', map {
            exists($hid->{Flags}{$_}) ? '1' : '0'
            } @allflags;
        print DEBUG "HID $hid->{Ident} PRIORITY $hid->{Priority}\n";
    }
    @hids= sort { $b->{Priority} cmp $a->{Priority} } @hids;

    my $flagscheckq= $dbh_tests->prepare(<<END);
        SELECT * FROM hostflags
         WHERE hostname = ? AND hostflag = ?
END

    my $recentflightsq= $dbh_tests->prepare(<<END);
            SELECT * FROM flights f
                     JOIN jobs j USING (flight)
                     JOIN runvars r
                             ON  f.flight=r.flight
                            AND  r.name='host'
                    WHERE  j.job=r.job
                      AND  f.blessing=?
                      AND  f.branch=?
                      AND  r.val=?
                 ORDER BY f.started desc
                    LIMIT 1
END

    my $sharingq= $dbh_tests->prepare(<<END);
            SELECT hostname, sharetype, state, wear,
                   (SELECT count(*) FROM host_sharing_tasks t
                     WHERE t.hostname = s.hostname) AS ntasks
              FROM host_sharing s WHERE hostname=?
END

    alloc_resources(sub {
        logm("allocating hosts: ".join(' ', map { $_->{Ident} } @hids));

        $dbh_tests->do(<<END);
            LOCK TABLE host_sharing_tasks IN ACCESS EXCLUSIVE MODE
END
        foreach my $hid (@hids) { delete $hid->{Allocated}; }

        foreach my $hid (@hids) {
            my $use= $r{$hid};
            my $shr= $hid->{Shared};
            my $findhostsq;
            
            if (defined $use) {
                print STDERR "HID $hid->{Ident} USE $use\n";
                $host1q->execute($use);
                $findhostsq= $host1q;
            } else {
                print STDERR "HID $hid->{Ident} INTENDED\n";
                $hostsq->execute("blessed-$fi->{intended}");
                $findhostsq= $hostsq;
            }
            my @candidates;
            my $any=0;

            while (my $candrow= $hostsq->fetchrow_hashref()) {
                $candrow->{Warnings}= [ ];
                
                my $dbg= "HID $hid->{Ident} HOST $candrow->{hostname}";
                print DEBUG "$dbg\n";
                my @nogood;
                foreach my $flag (keys %{ $hid->{Flags} }) {
                    print DEBUG "$dbg FLAG $flag\n";
                    $flagscheckq->execute($candrow->{hostname}, $flag);
                    next if $flagscheckq->fetchrow_arrayref();
                    push @nogood, $flag;
                }
                print DEBUG "$dbg NOGOOD @nogood\n";
                if (@nogood) {
                    next unless defined $use;
                    push @{ $candrow->{Warnings} },
                        "specified host lacks flags @nogood";
                }
                $any++;

                print DEBUG "$dbg GOOD\n";

                my $allocatable=
                    $candrow->{owntaskid} == $magictaskid{allocatable};

                print DEBUG "$dbg ALLOC PLAIN $allocatable\n";

                my $sharingrow;
                if ($candrow->{owntaskid} == $magictaskid{shared}) {
                    $sharingq->execute($candrow->{hostname});
                    $sharingrow= $sharingq->fetchrow_hashref();
                    die $candrow->{hostname} unless $sharingrow;

                    print DEBUG "$dbg ALLOC ISSHARED".
                        " $sharingrow->{sharetype} $sharingrow->{ntasks}".
                        " $sharingrow->{wear}\n";
                    
                    if (!$sharingrow->{ntasks}) {
                        $candrow->{Sharing}= $sharingrow;
                        $allocatable= 1;
                    }
                }

                if (defined $shr && defined $sharingrow &&
                    $sharingrow->{sharetype} eq $shr &&
                    $sharingrow->{ntasks} < $hid->{SharedMaxTasks} &&
                    $sharingrow->{wear} < $hid->{SharedMaxWear}) {
                    print DEBUG "$dbg ALLOC SHARED YES $sharingrow->{state}\n";
                    if ($sharingrow->{state} eq 'avail') {
                        $allocatable= 1;
                        $candrow->{SharingReuse}= 1;
                        $candrow->{Cost}= 0;
                    } elsif ($sharingrow->{ntasks}) {
                        # someone else is preparing it, we must wait
                        # no point thinking any more
                        logm("host $candrow->{hostname} shareable $shr".
                             " for $hid->{Ident} is being prepared".
                             " ($sharingrow->{state})");
                        return 0;
                    } else {
                        # someone was preparing it but they aren't any more
                        push @{ $candrow->{Warnings} },
                            "previous preparation apparently abandoned";
                        $candrow->{Cost}= 1;
                    }
                } else {
                    next unless $allocatable;
                    $recentflightsq->execute($fi->{intended},
                                             $fi->{branch},
                                             $candrow->{hostname});
                    my $recent= $recentflightsq->fetchrow_hashref();
                    $candrow->{Cost}=
                        defined($recent) ? $recent->{started} : 1;
                    print DEBUG "$dbg ALLOC PLAIN OK $candrow->{Cost}\n";
                }

                die unless $allocatable;
                push @candidates, $candrow;
            }

            print DEBUG "HID $hid->{Ident} CANDS ".scalar(@candidates)."\n";

            @candidates= sort { $a->{Cost} <=> $b->{Cost} } @candidates;
            if (!@candidates) {
                logm("none of $any hosts for $hid->{Ident} available");
                die "no possible hosts" unless $any;
                return 0;
            }

            # Right, allocate this one!
            my $candrow= $candidates[0];
            my $hn= $candrow->{hostname};

            print DEBUG "HID $hid->{Ident} YES YES $hn\n";

            if ($candrow->{SharingReuse}) {
                print DEBUG "HID $hid->{Ident} YES YES $hn REUSE\n";
                $claim_share_reuse_q->execute($hn);
            } elsif (defined $shr) {
                print DEBUG "HID $hid->{Ident} YES YES $hn NEW SHARE\n";
                $claim_setres_q->execute($magictaskid{shared}, $hn);
                $claim_unshare_q->execute($hn);
                $claim_share_new_q->execute($hn, $shr);
            }
            if (defined $shr) {
                print DEBUG "HID $hid->{Ident} YES YES $hn ADD SHARE\n";
                $claim_share_addtask_q->execute($taskid, $hn, "$flight.$job");
            } else {
                print DEBUG "HID $hid->{Ident} YES YES $hn PLAIN\n";
                $claim_setres_q->execute($taskid, $hn);
                $claim_unshare_q->execute($hn);
            }
            logm("allocating for $hid->{Ident} (from $any): $hn");
            foreach my $warn (@{ $candrow->{Warnings} }) {
                logm("warning about $hn: $warn");
            }

            $hid->{Allocated}= $hn;
        }

        print DEBUG "YES YES YES\n";
        return 1; # yay
    });

    foreach my $hid (@hids) {
        my $hn= $hid->{Allocated};;
        die $hid->{Ident} unless defined $hn;
            
        store_runvar($hid->{Ident}, $hn);
    }
}

$taskid= findtask();
alloc_hosts();
