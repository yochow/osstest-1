#!/usr/bin/perl -w

use strict qw(refs vars);

use DBI;
use Osstest;
use IO::Handle;
use HTML::Entities;
use URI::Escape;
use POSIX;
use IPC::Open2;

our $specflight;
our %specver;
our @blessings;
our $mro;
our $htmldir;
our $branch;
our @allows;
our @includefiles;

open DEBUG, ">/dev/null";
open MRO, ">/dev/null";

while (@ARGV && $ARGV[0] =~ m/^-/) {
    $_= shift @ARGV;
    last if m/^--?$/;
    if (m/^--(this|that)-([^=]+)\=([^=]+)$/) {
        $specver{$1}{$2}= $3;
    } elsif (m/^--machine-readable-output=(.*)$/) {
        $mro= $1;
        open MRO, "> $mro.new" or die "$mro.new $!";
    } elsif (m/^--html-dir=(.*)$/) {
        $htmldir= $1;
    } elsif (m/^--blessings?=(.*)$/) {
        push @blessings, split ',', $1;
    } elsif (m/^--include=(.*)$/) {
        push @includefiles, $1;
    } elsif (m/^--allow=(.*)$/) {
        my ($allowfile) = $1;
        if (!open A, '<', $allowfile) {
            die "$allowfile: $!" unless $! == &ENOENT;
        } else {
            while (<A>) {
                s/^\s+//; s/\s+$// or die;
                next unless m/\S/;
                next if m/^\#/;
                s/\s+/ /g;
                die if m/ /g > 1;
                $_ .= ' @@' unless m/ /;
                s/\@\@/ '[^ ]*' /ge;
                s/\@/ '[^ -\/]*' /ge;
                $_= '^(?:'.$_.')$';
                print DEBUG "ALLOW $_\n";
                push @allows, $_;
            }
            A->error and die "$allowfile $!";
            close A or die "$allowfile $!";
        }
    } elsif (m/^--debug/) {
        open DEBUG, ">&2" or die $!;
        DEBUG->autoflush(1);
    } else {
        die "$_ ?";
    }
}

@ARGV==1 or die;

$_= shift @ARGV;
if (m/^\d+$/) {
    $specflight= $_;
    print MRO "flight $specflight\n" or die $!;
} else {
    $branch= $_;
}

die if defined $specver{this}{flight};
die if defined $specver{that}{flight} &&
    grep { $_ ne 'flight' } keys %{ $specver{that} };

push @blessings, 'real' unless @blessings;

csreadconfig();

# formatting settings
our $jl= 28;
our $sl= 28;
our $rl= 9;
our $cw= 79;
our $tl= 20;
our $htmlleaf= "info.html";

our $blessingscond= '('.join(' OR ', map { "blessing=?" } @blessings).')';

sub findaflight ($$) {
    my ($thisthat, $sub) = @_;

    die $thisthat unless keys %{ $specver{$thisthat} };

    my $thatflight= $specver{that}{flight};
    if (defined $thatflight) {
        print DEBUG " $thatflight specified:\n";
        my $whynot= $sub->($thatflight);
        return $thatflight if !defined $whynot;
        print DEBUG " $thatflight $whynot\n";
        return undef;
    }

    my $flightsq= <<END;
        SELECT flight FROM flights
            WHERE branch=?
              AND $blessingscond
            ORDER BY flight DESC
            LIMIT 1000
END
    #print DEBUG "===\n$flightsq\n===\n";
    $flightsq= $dbh_tests->prepare($flightsq);
    $flightsq->execute($branch, @blessings);

    my $revisionsq= <<END;
            SELECT job, val FROM runvars
                WHERE flight=?
		  AND name=?
		GROUP BY job, val
END
    #print DEBUG "===\n$mismatchq\n===\n";
    $revisionsq= $dbh_tests->prepare($revisionsq);

    my $revisionsosstestq= $dbh_tests->prepare(<<END);
            SELECT harness AS val FROM flights_harness_touched
                WHERE flight=?
END

    while (my ($tflight) = $flightsq->fetchrow_array) {
	my $whynot;
	foreach my $tree (keys %{ $specver{$thisthat} }) {
	    my $v= $specver{$thisthat}{$tree};
            my $revisions;
            if ($tree ne 'osstest') {
                $revisionsq->execute($tflight, "built_revision_$tree");
                $revisions= $revisionsq->fetchall_arrayref({});
            } else {
                $revisionsosstestq->execute($tflight);
                $revisions= $revisionsosstestq->fetchall_arrayref({});
            }
            if (!@$revisions) {
                $whynot= "no built/used $tree";
                last;
            }
            my ($wrong) = grep {
                $_->{val} !~ m/^(?: .*: )? $v /x;
            } @$revisions;

            if (defined $wrong) {
                $whynot= "mismatch $tree ".
                    (defined $wrong->{job} ? $wrong->{job} : "(osstest)").
                    " $wrong->{val} != $v";
                last;
            }
	}
        $whynot= $sub->($tflight) if !defined $whynot;
        return $flight            if !defined $whynot;
        print DEBUG " $tflight $whynot\n";
    }
    return undef;
}

sub findspecflight () {
    if (defined $specflight) {
        ($branch) = $dbh_tests->selectrow_array(<<END, {}, $specflight);
            SELECT branch FROM flights WHERE flight=?
END
        die "$specflight branch ?" unless defined $branch;
    } else {
        $specflight= findaflight('this', sub { return undef; });
        die unless defined $specflight;
    }
}

sub examineflight ($) {
    my ($flight) = @_;
    my @jobtexts;
    my %fails;
    my %statuscounts;
    my $jobsummary= '';

    my @gridcol_job;
    my @gridrow_rowspec;
    my %grid;

    my $flightinfo= $dbh_tests->selectrow_hashref(<<END);
        SELECT * FROM flights
            WHERE flight=$flight
END

    my $jobs= $dbh_tests->selectall_arrayref(<<END, { Slice => {} });
        SELECT * FROM jobs
            WHERE flight=$flight
END

    my $colmap= sub {
        local ($_) = @_;
        $_= $_->{job};
        #printf STDERR "CM %40s | ", $_;
        s#\bx\w+$#_$&#;
        s#^(\w+)\-(.*)$# $1.' '.(join '-', reverse split /-/, $2).' '. $& #e;
        #printf STDERR " %40s\n", $_;
        return $_;
    };
    $jobs= [ sort { $colmap->($a) cmp $colmap->($b) } @$jobs ];

    my $stepsq= $dbh_tests->prepare(<<END);
        SELECT * FROM steps
            WHERE flight=$flight AND job=?
            ORDER BY stepno
END

    my %jobtestid2rowspec;

    my ($tsout,$tsin);
    my $tsortpid = open2($tsout, $tsin, 'tsort');
    defined $tsortpid or die $!;
    foreach my $j (@$jobs) {
        my $lasttid;

        my $id2spec= $jobtestid2rowspec{$j->{job}}= { };

        my %rowspeccounts;
        my $testid2rowspec= sub {
            my ($testid) = @_;
            my $rowspec= $testid;
            $rowspec =~ s/\(\d+\)$//;
            my $count= ++$rowspeccounts{$rowspec};
            $rowspec .= "[$count]" if $count>1;
            $id2spec->{$testid}= $rowspec;
            return $rowspec;
        };

        $stepsq->execute($j->{job});
        while (my $s= $stepsq->fetchrow_hashref()) {
            my $tid= $s->{testid};
            my $ref= $testid2rowspec->($tid);
            print $tsin "R$ref T$tid\n" or die $!;
            if (defined $lasttid) {
                print $tsin "T$lasttid T$tid\n" or die $!;
                print $tsin "T$lasttid R$ref\n" or die $!;
            }
            $lasttid= $tid;
        }
    }
    close $tsin or die $!;
    my $ts_gridrow= 0;
    my %ts_gridrow;
    for (;;) {
        my $item = <$tsout>;
        last unless defined $item;
        die unless chomp $item;
        next unless $item =~ s/^R//;
        $ts_gridrow{$item}= ++$ts_gridrow;
    }
    $tsout->error and die $!;
    close $tsout or die $!;
    waitpid $tsortpid, 0 or die $!;
    $? and die "tsort $?";

    foreach my $j (@$jobs) {
        push @gridcol_job, $j->{job};

	$statuscounts{$j->{status}}++;

	my $jobtext= sprintf "%s:\n", $j->{job};

        $stepsq->execute($j->{job});

        $jobsummary .= sprintf " %-60s %-${rl}s\n", $j->{job}, $j->{status};

        my $foundfail= 0;

        my $storefail= sub {
            my ($s) = @_;
            my $failkey= "$j->{job}.$s->{testid}";
            my $failval= {
                Job => $j,
                Step => $s,
            };
            $fails{$failkey}= $failval;
            return $failval;
        };

        my $gridrow= 0;

	while (my $s= $stepsq->fetchrow_hashref()) {
	    $jobtext .= sprintf "%2d %-${sl}s %-${rl}s\n",
	        $s->{stepno}, $s->{testid}, $s->{status};

            my $fi= { Job => $j, Step => $s };
            
            if ($s->{status} ne 'pass') {
                $fi= $storefail->($s);
                $foundfail= 1;
            }

            my $rowspec= $jobtestid2rowspec{$j->{job}}{$s->{testid}};

            my $mingridrow= $ts_gridrow{$rowspec};
            if ($s->{step} eq 'ts-logs-capture' && $mingridrow < 100) {
                  $mingridrow += 100;
              }
            $gridrow= $mingridrow if $gridrow < $mingridrow;

            for (;;) {
                if (!defined $gridrow_rowspec[$gridrow]) {
                    $gridrow_rowspec[$gridrow]= $rowspec;
                    last;
                }
                if ($gridrow_rowspec[$gridrow] eq $rowspec and
                    !defined $grid{ $j->{job} }[ $gridrow ]) {
                    last;
                }
                $gridrow++;
            }
            $grid{ $j->{job} }[ $gridrow ]= $fi;
	}

        if ($j->{status} ne 'pass' and !$foundfail) {
            $storefail->({
                stepno => '',
                status => "$j->{status}",
                testid => '<none executed>'
            });
        }

        my $revh= $dbh_tests->prepare(<<END);
            SELECT * FROM runvars
                WHERE flight=$flight AND job='$j->{job}'
                  AND name like 'built_revision_%'
                ORDER BY name
END
        $revh->execute();
        while (my $r= $revh->fetchrow_hashref()) {
            my $br= $r->{name};
            $br =~ s/^built_revision_// or die "$br ?";
            $jobtext .= sprintf " %-15s %-.20s\n", $br, $r->{val};
        }

	push @jobtexts, [ split /\n/, $jobtext ];
    }

    my $outcomes= join '/', sort keys %statuscounts;
    my $outcome;
    if ($outcomes eq 'pass') {
        $outcome= 'all pass';
    } elsif ($outcomes eq 'fail') {
        $outcome= 'ALL FAIL';
    } elsif ($outcomes =~ m,^(?:blocked/)?(?:fail/)?pass$,) {
        $outcome= 'FAIL';
    } else {
        $outcome= "trouble: $outcomes";
    }

    return {
        Flight => $flight,
        FlightInfo => $flightinfo,
        JobTexts => \@jobtexts,
        Failures => \%fails,
        StatusCounts => \%statuscounts,
        OutcomeSummary => $outcome,
        JobsSummary => $jobsummary,
        GridCols => \@gridcol_job,
        GridRows => \@gridrow_rowspec,
        GridGrid => \%grid
    };
}

sub printversions ($) {
    my ($thisthat) = @_;
    my ($sv) = $specver{$thisthat};
    foreach my $k (sort keys %$sv) {
        printf " %-${tl}s %s\n", $k, $sv->{$k} or die $!;
        print MRO "version $thisthat $k $sv->{$k}\n" or die $!;
    }
}

sub printout ($) {
    my ($r) = @_;
    print <<END or die $!;
$r->{Flight}: $r->{OutcomeSummary}

flight $r->{Flight} $branch $r->{FlightInfo}{blessing} [$r->{FlightInfo}{intended}]
$c{ReportHtmlPubBaseUrl}/$r->{Flight}/
END
    if (defined $r->{Overall}) {
        print "\n" or die $!;
        print $r->{Overall} or die $!;
    }

    foreach my $blockers (qw(-1 1 0)) {
        my $hdrtxt=
            $blockers==-1 ? "Tests which did not succeed:"
          : $blockers==0 ? 
"Tests which did not succeed, but are not blocking,\n".
"including regressions (tests previously passed) regarded as allowable:"
          : $blockers==1 ? "Tests which did not succeed and are blocking:"
          : die $blockers;
        foreach my $failk (sort keys %{ $r->{Failures} }) {
            my $failv= $r->{Failures}{$failk};

            my $isblocker=
                !keys %{ $specver{that} } ? -1 :
                $failv->{Blocker} ? 1 :
                0;

            next unless $isblocker == $blockers;
            if ($hdrtxt) {
                print <<END or die $!;

$hdrtxt
END
                $hdrtxt= '';
            }
            my $s= $failv->{Step};
            my $j= $failv->{Job};
            my $text= (sprintf" %-${jl}s %2s %-${sl}s %-${rl}s",
                       $j->{job}, $s->{stepno}, $s->{testid}, $s->{status});
            $text .= $failv->{Summary} if defined $failv->{Summary};
            $text =~ s/ *$//;
            while (length($text) > $cw) { last unless $text =~ s/(.* ) /$1/; }
            print $text, "\n";
        }
    }

    if (!%{ $r->{Failures} }) {
        print "All tests in this flight passed" or die $!;
    }
    print "\n" or die $!;

    if (keys %{ $specver{this} }) {
        print "version targeted for testing:\n" or die $!;
        printversions('this');
    }
    if (keys %{ $specver{that} }) {
        print "baseline version:\n" or die $!;
        printversions('that');
    }

    if (@includefiles) {
        print "\n","-"x60, "\n" or die $!;
    }
    foreach my $include (@includefiles) {
        STDOUT->flush();
        system_checked('cat','--',$include);
        print "-"x60, "\n" or die $!;
    }

    print "\njobs:\n", $r->{JobsSummary}, "\n" or die $!;

#    foreach my $jt (@{ $r->{JobTexts} }) {
#	print '-'x$cw or die $!;
#        print "\n" or die $!;
#        print join "\n", @$jt or die $!;
#        print "\n" or die $!;
#    }

    my $hostname= `hostname -f`;
    $? and die $?;  chomp $hostname;

    my $logs= $c{Logs};

    print <<END or die $!;

------------------------------------------------------------
sg-report-flight on $hostname
logs: $logs
images: $c{Images}

$c{ReportTrailer}
END
}

sub justifyfailures ($) {
    my ($fi) = @_;

    return unless keys %{ $specver{that} };

    my @failures= values %{ $fi->{Failures} };

    my $anypassq= <<END;
        SELECT * FROM flights JOIN steps USING (flight)
            WHERE branch=?
              AND job=? and testid=? and status='pass'
              AND $blessingscond
            LIMIT 1
END
    $anypassq= $dbh_tests->prepare($anypassq);

    my $duration_estimator= duration_estimator($branch, $blessings[0],
                                               sub { print DEBUG "== @_\n"; });
    foreach my $failv (@failures) {
        my ($est) = $duration_estimator->($failv->{Job}{job},'','');
        print DEBUG "DE $failv->{Job}{job} $est\n";
        $failv->{DurationEstimate}= $est || 1e5;
    }

    @failures= sort { $a->{DurationEstimate} <=> $b->{DurationEstimate} }
        @failures;

    foreach my $failv (@failures) {
        my $j= $failv->{Job};
        my $s= $failv->{Step};
        my $st= $s->{status};
        print DEBUG "justifying $j->{job} $s->{testid} $st\n";

        if (!length $s->{stepno}) {
            print MRO "broken-job $j->{job} $j->{status}\n" or die $!;
            print DEBUG " broken job\n";
            $failv->{Summary}= '';
            $failv->{Blocker}= 'broken';
            next;
        }

        $anypassq->execute($branch, $j->{job}, $s->{testid}, @blessings);
        if (!$anypassq->fetchrow_hashref()) {
            print MRO "never-passed $j->{job} $s->{testid} $st\n";
            print DEBUG " never passed\n";
            $failv->{Summary}= 'never pass';
            $failv->{Blocker}= '';
            next;
        }

	if ($st eq 'blocked') {
	    print MRO "blocked $s->{job} $s->{testid}\n";
	    print DEBUG " blocked, call it justifiable\n";
	    $failv->{Summary}= 'n/a';
            $failv->{Blocker}= '';
	    next;
	}
        if ($st ne 'fail') {
            print MRO "broken $j->{job} $s->{testid} $st\n";
            print DEBUG " not a fail, unjustifiable\n";
            $failv->{Summary}= '';
            $failv->{Blocker}= 'broken';
            next;
        }

	my @rtups;
	push @rtups, [ 10, 0, "baseline untested", undef ];

	findaflight('that', sub {
            my ($chkflight) = @_;
            my $checkq= $dbh_tests->prepare(<<END);
                SELECT status FROM steps WHERE flight=? AND job=? AND testid=?
END
            $checkq->execute($chkflight, $j->{job}, $s->{testid});
            my ($chkst) = $checkq->fetchrow_array();
	    my $rtup;
	    if (!defined $chkst) {
		$rtup= [ 50, '', "blocked in", $chkflight ];
	    } elsif ($chkst eq $st) {
		$rtup= [ 190, '', "like", $chkflight ];
	    } elsif ($chkst eq 'fail') {
		$rtup= [ 180, '', "failed in", $chkflight ];
	    } elsif ($chkst eq 'pass') {
		$rtup= [ 100, 'regression', "REGR. vs.", $chkflight ];
	    } else {
		$rtup= [ 170, '', "$chkst in", $chkflight ];
	    }
	    push @rtups, $rtup;
	    return $rtup->[2];
        });
	@rtups= sort { $b->[0] <=> $a->[0] } @rtups;
        my $blocker= $rtups[0][1];;
        if ($blocker) {
            my $item= "$failv->{Job}{job} $failv->{Step}{testid}";
            print DEBUG " allow $item?";
            foreach my $allow (@allows) {
                next unless $item =~ m/$allow/;
                $blocker='';
                print DEBUG " allowed";
                last;
            }
            print DEBUG "\n";
        }
	$failv->{Blocker}= $blocker;
	$failv->{SummaryCore}= $rtups[0][2];
	$failv->{SummaryRefFlight}= $rtups[0][3];
	$failv->{Summary}= $failv->{SummaryCore};
        $failv->{Summary}.= ' '.$failv->{SummaryRefFlight}
            if defined $failv->{SummaryRefFlight};
	print DEBUG " so $failv->{Summary}\n";
        print DEBUG " blocker $failv->{Blocker}\n" if $failv->{Blocker};
        if ($failv->{Blocker} eq 'regression') {
            print MRO "regression $j->{job} $s->{testid}".
                " $failv->{SummaryRefFlight}\n" or die $!;
            next;
        }
    }
    if (!@failures) {
        print MRO "tolerable\nperfect\n" or die $!;
         $fi->{Overall}.= "Perfect :-)\n";
    } elsif (grep { $_->{Blocker} eq 'regression' } @failures) {
        $fi->{OutcomeSummary}= "regressions - $fi->{OutcomeSummary}";
        $fi->{Overall}.= "Regressions :-(\n";
    } elsif (!grep { $_->{Blocker} } @failures) {
        $fi->{OutcomeSummary}= "tolerable $fi->{OutcomeSummary}";
        print MRO "tolerable\n" or die $!;
        $fi->{Overall}.= "Failures :-/ but no regressions.\n";
    } else {
        $fi->{Overall}.= "Failures and problems with tests :-(\n";
    }
}

sub htmloutjob ($$) {
    my ($fi,$job) = @_;
    return unless defined $htmldir;

    my $htmlfile= "$htmldir/$job/$htmlleaf";
    if (!open H, "> $htmlfile.new") {
        return if $!==&ENOENT;
        die "$htmlfile.new $!";
    }
    my $title= "Info on flight $fi->{Flight} job $job";
    my $branch= $fi->{FlightInfo}{branch};

    print H <<END;
<html><head><title>$title</title><head>
<body>
<h1>$title</h1>
<ul>
<li><a href="../">Flight $fi->{Flight} scoreboard</a>
<li><a href="$c{ResultsHtmlPubBaseUrl}/history.$job">History for $job</a>
<li><a href="$c{ResultsHtmlPubBaseUrl}/history.$job.$branch">History for $job in tests of $branch</a>
<li><a href="./">Logfiles for $fi->{Flight} $job as webserver directory listing</a>
</ul>
<h2>Steps</h2>
<table rules=all>
<tr><th>stepno</th><th>testid</th><th>script</th><th>status</th>
<th>started</th><th>duration</th></tr>
END

    my %issteplog;
    my $stepsq= $dbh_tests->prepare(<<END);
        SELECT * FROM steps
                WHERE flight=? AND job=?
             ORDER BY stepno ASC
END
    $stepsq->execute($fi->{Flight}, $job);
    my $stepstripe= 0;
    while (my $step= $stepsq->fetchrow_hashref()) {
        printf H "<tr bgcolor='%s'>",
            ($step->{stepno} & 1 ? '#cccccc' : '#ffffff');

        my $stepcol= sub {
            printf H "<td%s>%s</td>", $_[1], encode_entities($step->{$_[0]});
        };
        $stepcol->('stepno', ' align=right');
        $stepcol->('testid', '');
        $stepcol->('step', '');

        my $st= $step->{status};
        my $logfilename= "$step->{stepno}.$step->{step}.log";
        printf H "<td bgcolor='%s'><a href='%s'>%s</a></td>",
            html_status2colour($st),
            encode_entities(uri_escape($logfilename)),
            encode_entities($st);
        $issteplog{$logfilename}= 1;

        printf H "<td>%s</td>",
            encode_entities(strftime "%Y-%m-%d %H:%M:%S Z",
                            gmtime $step->{started});

        printf H "<td align=right>%d</td>\n",
            $step->{finished} - $step->{started};
    }

    print H <<END;
</table>
<h2>Logfiles etc.</h2>
For main test script logfiles, see entries in steps table.
<ul>
END
    opendir LOGS, "$htmldir/$job" or die "$htmldir/$job $!";
    my @dents= sort readdir LOGS;
    closedir LOGS or die $!;
    foreach my $dent (@dents) {
        next if $dent eq "$htmlleaf.new" or $dent eq $htmlleaf
            or $dent eq '..' or $dent eq '.' or $issteplog{$dent};
        stat("$htmldir/$job/$dent") or die "$dent $!";
        print H "<li><a href=\"".encode_entities(uri_escape($dent))."\">".
            encode_entities($dent).(-d _ ? "/" : "")."</a>\n";
        if ($dent eq 'build') {
            print H " (outputs from build)";
        }
    }
    print H <<END;
</ul>
<h2>Test control variables</h2>
<table rules=all><tr><th>Name</th><th>Value</th><th>Source</th></tr>
END
    my $varsq= $dbh_tests->prepare(<<END);
        SELECT * FROM runvars
                WHERE flight=? AND job=?
             ORDER BY synth, name
END
    $varsq->execute($fi->{Flight}, $job);
    while (my $varrow= $varsq->fetchrow_hashref()) {
        print H "<tr><th>".encode_entities($varrow->{name})."</th>";
        print H "<td>".encode_entities($varrow->{val})."</td>";
        print H "<td>".($varrow->{synth} ? 'execution' : 'definition')."</td>";
        print H "\n";
    }
    print H "</table></body></html>\n";

    H->error and die $!;
    close H or die $!;
    rename "$htmlfile.new", $htmlfile or die $!;
}

sub html_status2colour ($) {
    my ($st) = @_;
    return ($st eq 'pass' ? '#008800' :
            $st eq 'fail' ? '#ff8888' :
            '#ffff00');
}

sub htmlout ($) {
    my ($fi) = @_;
    return unless defined $htmldir;

    my @cols= @{ $fi->{GridCols} };

    htmloutjob($fi, $_) foreach @cols;

    my $htmlout= "$htmldir/index.html";
    open H, "> $htmlout.new" or die "$htmlout.new $!";
    print H <<END;
<html><head><title>Report on flight $fi->{Flight}</title><head>
<body>
<table rules=all>
END
    print H "<tr><th>";
    print H encode_entities($fi->{FlightInfo}{branch}).' ';
    print H encode_entities($fi->{FlightInfo}{blessing});
    print H "</th>\n";

    foreach my $col (@cols) {
        my $th= $col;
        $th =~ s/\-/ $&/g;
        print H "<th>";
        print H "<a href=\"".encode_entities($col)."/$htmlleaf\">";
        print H encode_entities($th);
        print H "</a>";
        print H "</th>";
    }
    print H "\n";
    foreach my $rowix (0..$#{ $fi->{GridRows} }) {
        next unless $fi->{GridRows}[$rowix];
        my $rowdesc= encode_entities($fi->{GridRows}[$rowix]);
        print H "<tr><th>$rowdesc</th>";

        my $ndeferred= 0;
        my $colix= 0;
        my $deferred_cells= sub {
            my ($now) = @_;

            my $min= 3;

            my $label;
            $label= sub {
                my ($nuse, $align) = @_;
                return unless $nuse;
                die unless $nuse <= $ndeferred;

                print H "<td bgcolor=\"#444444\" colspan=$nuse align=$align>";
                if ($nuse >= $min) {
                    print H $rowdesc;
                }
                print H "</td>";
                $ndeferred -= $nuse;
            };

            if ($colix>$ndeferred && $ndeferred >= ($now ? $min*2 : $min)) {
                $label->($min, 'left');
            }
            while ($ndeferred > $min) {
                $label->(1, 'right');
            }
            $label->($ndeferred, 'right');
        };

        foreach my $col (@cols) {
            my $ei= $fi->{GridGrid}{$col}[$rowix];
            if ($ei) {
                $deferred_cells->(1);
                print H "<td";
                my $s= $ei->{Step};
                my $sum_core= $ei->{SummaryCore};
                my $sum_flight= $ei->{SummaryRefFlight};
                $sum_core= $ei->{Summary} if !defined $sum_core;
                my $bgc;
                if (!defined $sum_core || !length $sum_core) {
                    $sum_core= $s->{status};
                    $bgc= html_status2colour($s->{status});
                } else {
                    $bgc= $ei->{Blocker} eq 'regression'
                        ? '#ff8888' : '#ffff00';
                }
                print H " bgcolor=\"$bgc\">";
                print H "<a href=\"".encode_entities($col).
                    "/$s->{stepno}.".encode_entities($s->{step}).".log\">";
                #print H "[$s->{stepno}] ";
                print H encode_entities($sum_core);
                print H "</a>";
                if (defined $sum_flight) {
                    print H " <a href=\"../$sum_flight/\">$sum_flight</a>";
                }
                print H "</td>";
            } else {
                $ndeferred++;
            }
            $colix++;
        }
        $deferred_cells->(0);
        print H "\n";
    }
    print H "</table>";

    my %vercols;
    foreach my $tt (qw(this that)) {
        my $ary= $specver{$tt};
        next unless $ary;
        $vercols{$_}=1 foreach keys %$ary;
    }
    my @vercols= sort keys %vercols;
    if (@vercols) {
        print H "<h2>targeted versions</h2>\n";
        print H "<table rules=all>";
        print H "<tr><td></td>";
        print H "<th>".encode_entities($_)."</th>" foreach @vercols;
        foreach my $tt (qw(this that)) {
            my $ary= $specver{$tt};
            next unless $ary;
            print H "<tr>";
            print H "<th>".($tt eq 'that' ? 'baseline' : $tt)."</th>";
            foreach my $vc (@vercols) {
                my $v= $ary->{$vc};
                print H "<td>";
                print H encode_entities($v) if defined $v;
                print H "</td>";
            }
        }
        print H "</table>";
    }
    print H "</body></html>";
    H->error and die $!;
    close H or die $!;
    rename "$htmlout.new", $htmlout or die $!;
}

db_begin_work($dbh_tests, [qw(flights)]);
findspecflight();
my $fi= examineflight($specflight);
justifyfailures($fi);
printout($fi);
htmlout($fi);

if (defined $mro) {
    close MRO or die $!;
    rename "$mro.new",$mro or die $!;
}
