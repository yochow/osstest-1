#!/usr/bin/perl -w
# This is part of "osstest", an automated testing framework for Xen.
# Copyright (C) 2009-2013 Citrix Inc.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict qw(vars);
use DBI;
use Osstest;
use Osstest::TestSupport;

tsreadconfig();

our ($whhost,$gn) = @ARGV;
$whhost ||= 'host';
$gn ||= 'freebsd';

our $ho= selecthost($whhost);

our $ram_mb=   1024;
our $disk_mb= 20480;

our $guesthost= "$gn.guest.osstest";
our $gho;

our $mnt= '/root/freebsd_root';

our $freebsd_version= "10.0-BETA3";

# Folder where the FreeBSD VM images are stored inside of the host
#
# The naming convention of the stored images is:
# FreeBSD-$freebsd_version-$arch.qcow2.xz
# ie: FreeBSD-10.0-BETA3-amd64.qcow2.xz
#
# Used only if the runvar <guest>_image is not set.
#
our $freebsd_vm_repo= '/var/images';

sub prep () {
    my $authkeys= authorized_keys();

    target_install_packages_norec($ho, qw(rsync lvm2 qemu-utils
                                          xz-utils kpartx));

    $gho= prepareguest($ho, $gn, $guesthost, 22,
                       $disk_mb + 1,
                       100);

    more_prepareguest_hvm($ho, $gho, $ram_mb, $disk_mb, NoCdromImage => 1);

    target_put_guest_image($ho, $gho,
			   "$freebsd_vm_repo/FreeBSD-$freebsd_version-".
			   (defined($r{"$gho->{Guest}_arch"})
			    # Use amd64 as default arch
			    ? $r{"$gho->{Guest}_arch"} : 'amd64').
			   ".qcow2.xz");
				
    my $rootpartition_dev = target_guest_lv_name($ho, $gho->{Name}) . "--disk3";

    target_cmd_root($ho, "umount $gho->{Lvdev} ||:");

    my $rimage = $gho->{Rimage};
    my $rimagebase = $rimage; $rimagebase =~ s/\.\w+$//;

    target_cmd_root($ho, <<END, 900);
            set -ex
            xz -dkc $rimage > $rimagebase.qcow2
            qemu-img convert -f qcow2 $rimagebase.qcow2 -O raw $rimagebase.raw
            rm $rimagebase.qcow2
            dd if=$rimagebase.raw of=$gho->{Lvdev} bs=1M
            rm $rimagebase.raw

            kpartx -a $gho->{Lvdev}
            mkdir -p $mnt
            mount -t ufs -o ufstype=ufs2,rw $rootpartition_dev $mnt
END

    target_cmd_root($ho, <<END, 900);
            mkdir -p $mnt/root/.ssh
            cat <<'ENDKEYS' >$mnt/root/.ssh/authorized_keys
$authkeys
ENDKEYS

END

    target_cmd_root($ho, <<END, 900);
            echo 'sshd_enable="YES"' >> $mnt/etc/rc.conf
            echo 'ifconfig_xn0="DHCP"' >> $mnt/etc/rc.conf
            echo 'PermitRootLogin yes' >> $mnt/etc/ssh/sshd_config

            sed -i '/^ttyu0/s/off/on/' $mnt/etc/ttys

            cp $mnt/usr/share/zoneinfo/Europe/London $mnt/etc/localtime

            cat <<'ENDKEYS' >$mnt/boot/loader.conf
boot_multicons="YES"
boot_serial="YES"
comconsole_speed="115200"
console="comconsole,vidconsole"
ENDKEYS

            umount $mnt
            rm -rf $mnt
            kpartx -d $gho->{Lvdev}
END

}

prep();
