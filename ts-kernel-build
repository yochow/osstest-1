#!/usr/bin/perl

use strict qw(vars);
use DBI;
use Osstest;

readconfig();
opendb_state();
our ($whhost) = @ARGV;
$whhost ||= 'host';
our $ho= selecthost($whhost);

my $leaf= "build.$flight.$job";
my $builddir= "/home/osstest/$leaf";

sub checkout () {
    target_cmd($ho, "rm -rf $builddir && mkdir $builddir", 600);

    build_clone($ho, 'xen', $builddir, 'xen-unstable');

    build_clone($ho, 'linux', $builddir, 'linux');

    if (length($r{tree_pq_linux})) {
        build_clone($ho, 'pq_linux', $builddir, 'linux/.hg/patches');
        
        target_cmd_build($ho, 1000, $builddir, <<END);
            cd $builddir/linux
            hg qpush -a
END
    }
}

sub config () {
    my $edscript= "$builddir/enable-xen-config.ed";
    target_putfilecontents_stash($ho,1000,<<'END',$edscript);
$
?^exit 0
i

setopt CONFIG_AGP n
setopt CONFIG_DRM n
setopt CONFIG_FB n
setopt CONFIG_VIDEO n
setopt CONFIG_FRAMEBUFFER n
setopt CONFIG_BOOTSPLASH n

setopt CONFIG_DEBUG_INFO y
setopt CONFIG_BLK_DEV_NBD m

.
w
q
END
    target_cmd_build($ho, 1000, $builddir, <<END);
        cp xen-unstable/enable-xen-config .
        ed <$edscript enable-xen-config
        cd linux
        make defconfig
        ../enable-xen-config .config
        yes '' | make oldconfig
END
}

sub build () {
    target_cmd_build($ho, 9000, $builddir, <<END);
	cd linux
        (make -j4 all modules 2>&1 && touch ../build-ok-stamp) |tee ../log
        test -f ../build-ok-stamp
        echo ok.
END
}

sub kinstall () {
    my $kernfile= 'arch/x86/boot/vmlinux-stripped';
    target_cmd_build($ho, 100, $builddir, <<END);
	mkdir -p dist/boot dist/lib/modules
	cd linux
	make -j4 INSTALL_MOD_PATH=$builddir/dist modules_install
	cp System.map $builddir/dist/boot/
	cp $kernfile $builddir/dist/boot/vmlinuz
	cp .config $builddir/dist/boot/config
END
    my $kernver= target_cmd_output($ho, <<END);
	cd $builddir/dist/lib/modules
	echo *
END
    die "$kernver ?" unless $kernver =~ m/^\d\S+$/;
    logm("built $kernver");

    target_cmd_build($ho, 100, $builddir, <<END);
	cd dist/boot
	for f in vmlinuz System.map config; do
		mv \$f \$f-$kernver
	done
END
}

checkout();
config();
build();
kinstall();
built_stash($ho, $builddir, 'dist', 'kerndist');
